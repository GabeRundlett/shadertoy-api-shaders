{
    "Shader": {
        "info": {
            "date": "1463125806",
            "description": "Just an accompanying shader to my cellular tiled tunnel example.",
            "flags": 0,
            "hasliked": 0,
            "id": "MdtSRl",
            "likes": 15,
            "name": "Cellular Tiled Tunnel 2",
            "published": 3,
            "tags": [
                "tunnel",
                "cell",
                "tile"
            ],
            "usePreview": 0,
            "username": "Shane",
            "viewed": 1453
        },
        "renderpass": [
            {
                "code": "/*\n    Cellular Tiled Tunnel 2\n    -----------------------\n    \n    Creating a 2nd order Voronoi feel with minimal instructions by way of a 3D tile constructed via a\n\tsimplistic cellular pattern algorithm.\n\n\tThis is just an accompanying shader to my \"Cellular Tiled Tunnel\" example, and was patched togther\n    fairly quickly. I wanted to use the cellular tiles to create a more natual surface, albeit slightly \n\tstylized. I also added in a firey afterglow and a swiftly moving camera - just like an elite demo... \n\tfrom the late 90s. :) The cubic tile is being reused to create the lame, firey, volumetric haze... \n\tor whatever it's supposed to be. There are much better ways to go about it.\n\n\tJust for the fun, I kept the example textureless. Everything is generated with either tiles or \n\tsimple value noise. To keep the framerate up, I raymarched one layer of cellular tiling, then \n\tbump mapped the finer layers. For anyone interested, comment out the bump mapping and compare the\n\tsurface to a regular 2nd Order Voronoi surface. It looks pretty similar, but is considerably \n\tquicker to produce.\n\n    Related examples: \n\n    Cellular Tiling - Shane\n    https://www.shadertoy.com/view/4scXz2\n\n\tCellular Tiled Tunnel - Shane\n\thttps://www.shadertoy.com/view/MscSDB\n\n*/\n\n#define PI 3.14159265\n#define FAR 50.\n\n\n// Standard 1x1 hash functions. Using \"cos\" for non-zero origin result.\nfloat hash(float n){ return fract(cos(mod(n, 6.2831589))*45758.5453); }\n\n// Non-standard vec3-to-vec3 hash function.\nvec3 hash33(vec3 p){ \n    \n    float n = sin(mod(dot(p, vec3(7, 157, 113)), 6.2831589));    \n    return fract(vec3(2097152, 262144, 32768)*n); \n}\n\n// 2D rotation formula.\nmat2 rot2(float a){ float c = cos(a), s = sin(a); return mat2(c, s, -s, c); }\n\n\n// More concise, self contained version of IQ's original 3D noise function.\nfloat noise3D(in vec3 p){\n    \n    // Just some random figures, analogous to stride. You can change this, if you want.\n\tconst vec3 s = vec3(27, 57, 113);\n\t\n\tvec3 ip = floor(p); // Unique unit cell ID.\n    \n    // Setting up the stride vector for randomization and interpolation, kind of. \n    // All kinds of shortcuts are taken here. Refer to IQ's original formula.\n    vec4 h = vec4(0., s.yz, s.y + s.z) + dot(ip, s);\n    \n\tp -= ip; // Cell's fractional component.\n\t\n    // A bit of cubic smoothing, to give the noise that rounded look.\n    p = p*p*(3. - 2.*p);\n    \n    // Standard 3D noise stuff. Retrieving 8 random scalar values for each cube corner,\n    // then interpolating along X. There are countless ways to randomize, but this is\n    // the way most are familar with: fract(sin(x)*largeNumber).\n    h = mix(fract(sin(mod(h, 6.2831589))*43758.5453), \n            fract(sin(mod(h + s.x, 6.2831589))*43758.5453), p.x);\n\t\n    // Interpolating along Y.\n    h.xy = mix(h.xz, h.yw, p.y);\n    \n    // Interpolating along Z, and returning the 3D noise value.\n    return mix(h.x, h.y, p.z); // Range: [0, 1].\n\t\n}\n\n////////\n// The cellular tile routine. Draw a few objects (four spheres, in this case) using a minumum\n// blend at various 3D locations on a cubic tile. Make the tile wrappable by ensuring the \n// objects wrap around the edges. That's it.\n//\n// Believe it or not, you can get away with as few as three spheres. If you sum the total \n// instruction count here, you'll see that it's way, way lower than 2nd order 3D Voronoi.\n// Not requiring a hash function provides the biggest benefit, but there is also less setup.\n// \n// The result isn't perfect, but 3D cellular tiles can enable you to put a Voronoi looking \n// surface layer on a lot of 3D objects for little cost.\n//\nfloat drawSphere(in vec3 p){\n  \n    p = fract(p)-.5;    \n    return dot(p, p);\n    \n    //p = abs(fract(p)-.5);\n    //return dot(p, vec3(.5));  \n}\n\n\nfloat cellTile(in vec3 p){\n    \n    // Draw four overlapping objects (spheres, in this case) at various positions throughout the tile.\n    vec4 v, d; \n    d.x = drawSphere(p - vec3(.81, .62, .53));\n    p.xy = vec2(p.y-p.x, p.y + p.x)*.7071;\n    d.y = drawSphere(p - vec3(.39, .2, .11));\n    p.yz = vec2(p.z-p.y, p.z + p.y)*.7071;\n    d.z = drawSphere(p - vec3(.62, .24, .06));\n    p.xz = vec2(p.z-p.x, p.z + p.x)*.7071;\n    d.w = drawSphere(p - vec3(.2, .82, .64));\n\n    v.xy = min(d.xz, d.yw), v.z = min(max(d.x, d.y), max(d.z, d.w)), v.w = max(v.x, v.y); \n   \n    d.x =  min(v.z, v.w) - min(v.x, v.y); // Maximum minus second order, for that beveled Voronoi look. Range [0, 1].\n    //d.x =  min(v.x, v.y);\n        \n    return d.x*2.66; // Normalize... roughly.\n    \n}\n\n// The path is a 2D sinusoid that varies over time, depending upon the frequencies, and amplitudes.\nvec2 path(in float z){ \n    //return vec2(0);\n    float a = sin(z * 0.11);\n    float b = cos(z * 0.14);\n    return vec2(a*4. -b*1.5, b*1.7 + a*2.); \n}\n\n\n\n// Standard perturbed tunnel function.\n//\nfloat map(vec3 p){\n    \n   \n    float sf = cellTile(p*.25); // Cellular layer.\n    \n/*    \n     p.xy -= path(p.z); // Move the scene around a sinusoidal path.\n     p.xy = rot2(p.z/12.)*p.xy; // Twist it about XY with respect to distance.\n    \n     float n = dot(sin(p*1. + sin(p.yzx*.5 + iTime*.0)), vec3(.25)); // Sinusoidal layer.\n     \n     return 2. - abs(p.y) + n + (.5-sf)*.25; // Warped double planes, \"abs(p.y),\" plus surface layers.\n*/\n\n     float n = dot(sin(p*.5 + sin(p.yzx)), vec3(.5));\n    \n     // Standard tunnel. Comment out the above first.\n     return 3. - length(p.xy - path(p.z)) - sf +  n;\n\n \n}\n\n\n// Surface bump function. Cheap, but with decent visual impact.\nfloat bumpSurf3D( in vec3 p){\n    \n    \n    float noi = noise3D(p*25.)*.67 + noise3D(p*50.)*.33;\n    float vor = cellTile(p*.75)*.66 + cellTile(p*1.5)*.34;\n    \n    return vor*.9 + noi*.1;\n\n}\n\n// Standard function-based bump mapping function.\nvec3 doBumpMap(in vec3 p, in vec3 nor, float bumpfactor){\n    \n    const vec2 e = vec2(0.001, 0);\n    float ref = bumpSurf3D(p);                 \n    vec3 grad = (vec3(bumpSurf3D(p - e.xyy),\n                      bumpSurf3D(p - e.yxy),\n                      bumpSurf3D(p - e.yyx) )-ref)/e.x;                     \n          \n    grad -= nor*dot(nor, grad);          \n                      \n    return normalize( nor + grad*bumpfactor );\n\t\n}\n\n// Basic raymarcher.\nfloat trace(in vec3 ro, in vec3 rd){\n\n    float t = 0.0, h;\n    for(int i = 0; i < 80; i++){\n    \n        h = map(ro+rd*t);\n        // Note the \"t*b + a\" addition. Basically, we're putting less emphasis on accuracy, as\n        // \"t\" increases. It's a cheap trick that works in most situations... Not all, though.\n        if(abs(h)<.001*(t*.1 + 1.) || t>FAR) break; // Alternative: 0.001*max(t*.25, 1.)\n        t += h*.8;\n        \n    }\n\n    return min(t, FAR);\n}\n\n// Standard normal function. It's not as fast as the tetrahedral calculation, but more symmetrical.\nvec3 getNormal(in vec3 p) {\n\tconst vec2 e = vec2(.002, 0);\n\treturn normalize(vec3(map(p + e.xyy) - map(p - e.xyy),\n                          map(p + e.yxy) - map(p - e.yxy),\t\n                          map(p + e.yyx) - map(p - e.yyx)));\n}\n\n\n/*\n// Shadows.\nfloat softShadow(vec3 ro, vec3 rd, float start, float end, float k){\n\n    float shade = 1.0;\n    const int maxIterationsShad = 24;\n\n    float dist = start;\n    //float stepDist = end/float(maxIterationsShad);\n\n    // Max shadow iterations - More iterations make nicer shadows, but slow things down.\n    for (int i=0; i<maxIterationsShad; i++){\n    \n        float h = map(ro + rd*dist);\n        shade = min(shade, k*h/dist);\n\n        // +=h, += min(h, stepDist), +=min( h, 0.1 ), +=stepDist, +=min(h, stepDist*2.), etc.\n        dist += clamp(h, 0.02, 0.25 );\n        \n        // Early exits from accumulative distance function calls tend to be a good thing.\n        if (h<0. || dist > end) break; \n    }\n\n    // Shadow value.\n    return min(max(shade, 0.) + .1, 1.); \n}\n*/\n\n/*\n// Ambient occlusion, for that self shadowed look. Based on the original by XT95. I love this \n// function, and in many cases, it gives really, really nice results. For a better version, and \n// usage, refer to XT95's examples below:\n//\n// Hemispherical SDF AO - https://www.shadertoy.com/view/4sdGWN\n// Alien Cocoons - https://www.shadertoy.com/view/MsdGz2\nfloat calcAO( in vec3 p, in vec3 n )\n{\n\tfloat ao = 0.0, l;\n    const float maxDist = 2.;\n\tconst float nbIte = 6.0;\n\t//const float falloff = 0.9;\n    for( float i=1.; i< nbIte+.5; i++ ){\n    \n        l = (i*.75 + hash(i)*.25)/nbIte*maxDist;\n        \n        ao += (l - map( p + n*l ))/(1.+ l);// / pow(1.+l, falloff);\n    }\n\t\n    return clamp(1.- ao/nbIte, 0., 1.);\n}\n*/\n\n// I keep a collection of occlusion routines... OK, that sounded really nerdy. :)\n// Anyway, I like this one. I'm assuming it's based on IQ's original.\nfloat calcAO(in vec3 pos, in vec3 nor)\n{\n\tfloat sca = 2., occ = 0.;\n    for( int i=0; i<5; i++ ){\n    \n        float hr = 0.01 + float(i)*.7/4.;        \n        float dd = map(nor * hr + pos);\n        occ += (hr - dd)*sca;\n        sca *= .7;\n    }\n    return clamp(1. - occ, 0., 1.);    \n}\n\n\n\n/////\n// Code block to produce some layers of smokey haze. Not sophisticated at all.\n// If you'd like to see a much more sophisticated version, refer to Nitmitz's\n// Xyptonjtroz example. Incidently, I wrote this off the top of my head, but\n// I did have that example in mind when writing this.\n\n// Hash to return a scalar value from a 3D vector.\nfloat hash31(vec3 p){ \n    return fract(sin(mod(dot(p, vec3(127.1, 311.7, 74.7)), 6.2831589))*43758.5453); \n}\n\n// Four layers of cheap cell tile noise to produce some subtle mist.\n// Start at the ray origin, then take four samples of noise between it\n// and the surface point. Apply some very simplistic lighting along the \n// way. It's not particularly well thought out, but it doesn't have to be.\nfloat getMist(in vec3 ro, in vec3 rd, in vec3 lp, in float t){\n\n    float mist = 0.;\n    ro += rd*t/64.; // Edge the ray a little forward to begin.\n    \n    for (int i = 0; i<8; i++){\n        // Lighting. Technically, a lot of these points would be\n        // shadowed, but we're ignoring that.\n        float sDi = length(lp-ro)/FAR; \n\t    float sAtt = min(1./(1. + sDi*0.25 + sDi*sDi*0.25), 1.);\n\t    // Noise layer.\n        //float n = trigNoise3D(ro/2.);//noise3D(ro/2.)*.66 + noise3D(ro/1.)*.34;\n        float n = cellTile(ro/2.);\n        mist += n*sAtt;//trigNoise3D\n        // Advance the starting point towards the hit point.\n        ro += rd*t/8.;\n    }\n    \n    // Add a little noise, then clamp, and we're done.\n    return clamp(mist/4. + hash31(ro)*0.2-0.1, 0., 1.);\n\n}\n//////\n\n\nvec3 RandomSphereDir(vec2 rnd){\n\n\tfloat s = rnd.x * 3.141592653 * 2.0;\n\tfloat t = rnd.y * 2.0 - 1.0;\n\treturn vec3(sin(s), cos(s), t) / sqrt(1. + t*t);\n}\n\n// Standard semi hemispherical direction calculation.\nvec3 RandomHemisphereDir(vec3 dir, float i){\n\n\tvec3 v = RandomSphereDir(vec2(hash(i + 1.0), hash(i + 2.0)));\n\treturn v*sign(dot(v, dir));\n}\n\n\n/*\n// A mix between ZigguratVertigo's local thickness calculation and XT95's - Basically the same \n// as XT95's, but it gives the kind of results I was after.\n// Sources:\n// Local Thickness - ZigguratVertigo: https://www.shadertoy.com/view/ldGGzR\n// Alien Cocoons - XT95: https://www.shadertoy.com/view/MsdGz2\nfloat thickness(vec3 p, vec3 n)\n{\n    float thickness = 0.0;\n    \n    \n    const float nbIte = 6.;\n    const float maxDist = .3;\n    const float rad = 1.-1./nbIte; //Hemispherical factor (self occlusion correction).\n   \n    for (float i=0.; i < nbIte; i++)\n    {\n        // Randomly sample along the hemisphere inside the surface.\n        //float l = hash(float(i)) * maxDist;\n        float l = (i*.75 + fract(cos(i)*45758.5453)*.25)*maxDist;\n        vec3 rd = normalize(-n + RandomHemisphereDir(-n, l)*rad);\n        \n        // Accumulate.\n        thickness += (l + map(p + rd*l));\n    }\n\t\n    return max(thickness, 0.);\n}\n*/\n\n\n// Using a variation of IQ's AO function to calculate thickness, but with the normal flipped.\n// IQ uses a similar setup to calculate SSS... Subsurface AO, I guess you'd call it.\nfloat thickness(in vec3 p, in vec3 n){\n    \n    float sNum = 4.;\n    float sca = 1., occ = 0.;\n    for(float i=0.; i<sNum + .001; i++ ){\n    \n        float hr = .05 + .4*i/sNum; \n        //vec3 rn = normalize(n + RandomHemisphereDir(n, hr)*rad*.5);\n        float dd = map(p - n*hr);\n        occ += (hr - min(dd, 0.))*sca;\n        sca *= .9;\n    }\n    return 1. - max(occ/sNum, 0.); \n    \n}\n\n\n/*\n// XT95's really clever, cheap, SSS function. The way I've used it doesn't do it justice,\n// so if you'd like to really see it in action, have a look at the following:\n//\n// Alien Cocoons - XT95: https://www.shadertoy.com/view/MsdGz2\n//\nfloat thickness( in vec3 p, in vec3 n, float maxDist, float falloff )\n{\n\tconst float nbIte = 6.0;\n\tfloat ao = 0.0;\n    \n    for( float i=1.; i< nbIte+.5; i++ ){\n        \n        float l = (i*.75 + fract(cos(i)*45758.5453)*.25)/nbIte*maxDist;\n        \n        ao += (l + map( p -n*l )) / pow(1. + l, falloff);\n    }\n\t\n    return clamp( 1.-ao/nbIte, 0., 1.);\n}\n*/\n\nvoid mainImage( out vec4 fragColor, in vec2 fragCoord ){\n\t\n\t// Screen coordinates.\n\tvec2 uv = (fragCoord - iResolution.xy*0.5)/iResolution.y;\n\t\n\t// Camera Setup.\n\tvec3 lookAt = vec3(0., 0.0, iTime*5. + 0.1);  // \"Look At\" position.\n\tvec3 camPos = lookAt + vec3(0.0, 0.0, -0.1); // Camera position, doubling as the ray origin.\n\n \n    // Light positioning. \n \tvec3 lightPos = camPos + vec3(0, 0, 6);// Put it a bit in front of the camera.\n\n\t// Using the Z-value to perturb the XY-plane.\n\t// Sending the camera, \"look at,\" and two light vectors down the tunnel. The \"path\" function is \n\t// synchronized with the distance function. Change to \"path2\" to traverse the other tunnel.\n\tlookAt.xy += path(lookAt.z);\n\tcamPos.xy += path(camPos.z);\n\tlightPos.xy += path(lightPos.z);\n\n    // Using the above to produce the unit ray-direction vector.\n    float FOV = PI/2.; // FOV - Field of view.\n    vec3 forward = normalize(lookAt-camPos);\n    vec3 right = normalize(vec3(forward.z, 0., -forward.x )); \n    vec3 up = cross(forward, right);\n\n    // rd - Ray direction.\n    vec3 rd = normalize(forward + FOV*uv.x*right + FOV*uv.y*up);\n    \n    //vec3 rd = normalize(forward + FOV*uv.x*right + FOV*uv.y*up);\n    //rd = normalize(vec3(rd.xy, rd.z - dot(rd.xy, rd.xy)*.25));    \n    \n    // Swiveling the camera about the XY-plane (from left to right) when turning corners.\n    // Naturally, it's synchronized with the path in some kind of way.\n\trd.xy = rot2( path(lookAt.z).x/16. )*rd.xy;\n\t\t\n    // Standard ray marching routine. I find that some system setups don't like anything other than\n    // a \"break\" statement (by itself) to exit. \n\tfloat t = trace(camPos, rd);\n\t\n    // Initialize the scene color.\n    vec3 sceneCol = vec3(0);\n\t\n\t// The ray has effectively hit the surface, so light it up.\n\tif(t<FAR){\n\t\n   \t\n    \t// Surface position and surface normal.\n\t    vec3 sp = t * rd+camPos;\n\t    vec3 sn = getNormal(sp);\n        \n        \n        // Function based bump mapping. Comment it out to see the under layer. It's pretty\n        // comparable to regular beveled Voronoi... Close enough, anyway.\n        sn = doBumpMap(sp, sn, .5);\n\t    \n\t    // Ambient occlusion.\n\t    float ao = calcAO(sp, sn);\n    \t\n    \t// Light direction vectors.\n\t    vec3 ld = lightPos-sp;\n\n        // Distance from respective lights to the surface point.\n\t    float distlpsp = max(length(ld), .001);\n    \t\n    \t// Normalize the light direction vectors.\n\t    ld /= distlpsp;\n\t    \n\t    // Light attenuation, based on the distances above.\n\t    float atten = 1./(1. + distlpsp*0.25); // + distlpsp*distlpsp*0.025\n    \t\n    \t// Ambient light.\n\t    float ambience = .3;\n    \t\n    \t// Diffuse lighting.\n\t    float diff = max( dot(sn, ld), 0.);\n        \n   \t\n    \t// Specular lighting.\n\t    float spec = pow(max( dot( reflect(-ld, sn), -rd ), 0.0 ), 32.);\n\n\t    \n\t    // Fresnel term. Good for giving a surface a bit of a reflective glow.\n        float fre = pow( clamp(dot(sn, rd) + 1., .0, 1.), 1.);\n        \n        // Obtaining the texel color. \n        //vec3 ref = reflect(sn, rd);\n        \n       \n        // Object texturing. Two second texture algorithm. Terrible, but it's dark, so no one will notice. :)\n        vec3 texCol = vec3(.5, .6, .8);\n        \n        // Using layers from the map function to artificially darken the walls.\n        float sfu = cellTile(sp*.25); // Cellular layer.\n        float nf = dot(sin(sp*.5 + sin(sp.yzx)), vec3(1./6.)) + .5;\n        texCol *= smoothstep(0., .35, min(sfu, nf)) + .5;\n        // Doing the same with the bump function value.\n        float bFunc = bumpSurf3D(sp);\n        texCol *= smoothstep(-.1, .5, bFunc)*.7 + .3; // Darkening the crevices.\n \n        // Extra coloring.\n        texCol *= (noise3D(sp*32.)*.66 + noise3D(sp*64.)*.34)*(1. - cellTile(sp*16.)*.75);\n        texCol *= mix(vec3(.2, .4, 1), vec3(.6, 1, .2), min(sfu*nf, bFunc))*1.2 + .2;\n        texCol *= abs(sn)*.5 + .75; // Mild, lame, pearlescence.\n    \t\n        /////////   \n        // Translucency, courtesy of XT95. See the \"thickness\" function.\n        vec3 hf = normalize(ld + sn);\n        float th = thickness(sp, sn);\n        //float th = thickness( sp, sn, 1., 1. );\n        float tdiff =  pow( clamp( dot(rd, -hf), 0., 1.), 1.);\n        float trans = (tdiff + .125)*th;  \n        trans = pow(trans, 2.)*16.;        \n    \t////////        \n\n    \t\n    \t// Darkening the crevices. Otherwise known as cheap, scientifically-incorrect shadowing.\t\n\t    float shading = 1.;//crv*0.5+0.5; \n    \t\n        // Shadows - They didn't add enough aesthetic value to justify the GPU drain, so they\n        // didn't make the cut.\n        //shading *= softShadow(sp, ld, 0.05, distlpsp, 8.);\n    \t\n    \t// Combining the above terms to produce the final color. It was based more on acheiving a\n        // certain aesthetic than science.\n        sceneCol = texCol*(diff*diff + ambience + vec3(1, .9, .85)*spec*4.);// + vec3(.5, .8, 1)*spec2;\n        sceneCol += texCol*vec3(.8, .95, 1)*pow(fre, 4.)*4.;\n        sceneCol += texCol*vec3(1, .025, .05)*trans;\n        \n        //vec3 refCol = vec3(.7, .9, 1)*smoothstep(.25, 1., \n        //              noise3D((sp + ref*2.)*4.)*.66 + noise3D((sp + ref*2.)*8.)*.34 );\n        //sceneCol += refCol*.5;\n        \n        \n\n\t    // Shading.\n        sceneCol *= atten*shading*ao;\n\t   \n\t\n\t}\n       \n    // Blend the scene and the background with some very basic, 8-layered smokey haze.\n    float mist = getMist(camPos, rd, lightPos, t);\n    vec3 sky = vec3(2, 1, .85)*mix(1., .75, mist);//*(rd.y*.25 + 1.);\n    sceneCol = mix(sky, sceneCol, 1./(t*t/FAR/FAR*16. + 1.));\n\n    // Clamp and present the pixel to the screen.\n\tfragColor = vec4(sqrt(clamp(sceneCol, 0., 1.)), 1.0);\n\t\n}",
                "description": "",
                "inputs": [],
                "name": "Image",
                "outputs": [
                    {
                        "channel": 0,
                        "id": 37
                    }
                ],
                "type": "image"
            }
        ],
        "ver": "0.1"
    }
}