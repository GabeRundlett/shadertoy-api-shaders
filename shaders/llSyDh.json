{
    "Shader": {
        "info": {
            "date": "1508593594",
            "description": "A Truchet pattern flowing through a hexagonal maze. It's a 2D effect rendered in a faux 3D style.",
            "flags": 0,
            "hasliked": 0,
            "id": "llSyDh",
            "likes": 278,
            "name": "Hexagonal Maze Flow",
            "published": 3,
            "tags": [
                "hexagon",
                "truchet",
                "flow",
                "geometry",
                "pattern",
                "polar"
            ],
            "usePreview": 0,
            "username": "Shane",
            "viewed": 12132
        },
        "renderpass": [
            {
                "code": "/*\n\n\tHexagonal Maze Flow\n\t-------------------\n\n\tI've been playing around with hexagonal grids lately. It's possible to make all kinds of\n\tinteresting things with them. I'll eventually tire of hexagons and get back to what I'm \n\tsupposed to be doing, but for now, here's a maze with a flowing polkadot snake-like\n\tpattern running through it... I'm not really sure what it is either. :)\n\n\tIt has an impossible geometry feel to it, and is rendered in an oldschool kind of game\n\tstyle. It was pretty easy to produce: Obtain some hexagonal grid cell coordinates and \n\tcorresponding ID, render some lines, shapes, and a few arcs for the Truchet component,\n\tapply some shading, etc.\n\t\n\tIt was more of an exercise in applying layers in the right order than anything else. One \n\tof the things that might be of interest is the flowing Truchet pattern. Producing a \n\tflowing hexagonal Truchet isn't much different to producing a square one. In fact, it's\n\teasier - in the sense that you don't have to reverse directions from cell to cell. \n\n\tHowever, one of the downsides is that it's not immediately obvious how to apply UV \n\tcoordinates - I think BigWings mentioned this, and I concur. :) I got around the problem\n\tby ensuring that the texture pattern had the appropriate symmetry and applying hybrid \n\tpolar coordinates. By that, I mean, I wrapped the polar angles across the arc boundaries\n\tand used that for one coordinate, then used the Truchet distance field itself for the \n\tother...\n\n    Roughly speaking, it's not much different to a circle: In that situation, you use the \n\tfamiliar polar coordinates (r = length(u), a = atan(u.y, u.x)). The only difference in \n\tthis case is that you use a modified radial coordinate (r = min(min(r1, r2), r3)) - or \n\tto put it another way, you use the distance field value... It's a bit difficult to \n\texplain, but easy to perform. When I get time, I'm going to produce a more robust Truchet \n\ttexturing example.\n\t\n\n\tRelated references:\n\n\t// You can't do a hexagonal grid example without referencing this. :) Very stylish.\n\tHexagons - distance - iq\n\thttps://www.shadertoy.com/view/Xd2GR3\n\t\n\n\t// Simpler hexagonal grid example that attempts to explain the grid setup used to produce \n\t// the pattern here.\n\t//\n\tMinimal Hexagonal Grid - Shane\n\thttps://www.shadertoy.com/view/Xljczw\n\n*/\n\n// Interlaced variation - Interesting, but patched together in a hurry.\n//#define INTERLACING\n\n// A quick hack to get rid of the winding overlay - in order to show the maze only.\n//#define MAZE_ONLY\n\n\n\n// Helper vector. If you're doing anything that involves regular triangles or hexagons, the\n// 30-60-90 triangle will be involved in some way, which has sides of 1, sqrt(3) and 2.\nconst vec2 s = vec2(1, 1.7320508);\n\n// Standard vec2 to float hash - Based on IQ's original.\nfloat hash21(vec2 p){ return fract(sin(dot(p, vec2(141.173, 289.927)))*43758.5453); }\n\n\n// Standard 2D rotation formula.\nmat2 r2(in float a){ float c = cos(a), s = sin(a); return mat2(c, -s, s, c); }\n\n\n// The 2D hexagonal isosuface function: If you were to render a horizontal line and one that\n// slopes at 60 degrees, mirror, then combine them, you'd arrive at the following.\nfloat hex(in vec2 p){\n    \n    p = abs(p);\n    \n    // Below is equivalent to:\n    //return max(p.x*.5 + p.y*.866025, p.x); \n\n    return max(dot(p, s*.5), p.x); // Hexagon.\n    \n}\n\n// This function returns the hexagonal grid coordinate for the grid cell, and the corresponding \n// hexagon cell ID - in the form of the central hexagonal point. That's basically all you need to \n// produce a hexagonal grid.\n//\n// When working with 2D, I guess it's not that important to streamline this particular function.\n// However, if you need to raymarch a hexagonal grid, the number of operations tend to matter.\n// This one has minimal setup, one \"floor\" call, a couple of \"dot\" calls, a ternary operator, etc.\n// To use it to raymarch, you'd have to double up on everything - in order to deal with \n// overlapping fields from neighboring cells, so the fewer operations the better.\nvec4 getHex(vec2 p){\n    \n    // The hexagon centers: Two sets of repeat hexagons are required to fill in the space, and\n    // the two sets are stored in a \"vec4\" in order to group some calculations together. The hexagon\n    // center we'll eventually use will depend upon which is closest to the current point. Since \n    // the central hexagon point is unique, it doubles as the unique hexagon ID.\n    vec4 hC = floor(vec4(p, p - vec2(.5, 1))/s.xyxy) + .5;\n    \n    // Centering the coordinates with the hexagon centers above.\n    vec4 h = vec4(p - hC.xy*s, p - (hC.zw + .5)*s);\n    \n    // Nearest hexagon center (with respect to p) to the current point. In other words, when\n    // \"h.xy\" is zero, we're at the center. We're also returning the corresponding hexagon ID -\n    // in the form of the hexagonal central point. Note that a random constant has been added to \n    // \"hC.zw\" to further distinguish it from \"hC.xy.\"\n    //\n    // On a side note, I sometimes compare hex distances, but I noticed that Iomateron compared\n    // the Euclidian version, which seems neater, so I've adopted that.\n    return dot(h.xy, h.xy)<dot(h.zw, h.zw) ? vec4(h.xy, hC.xy) : vec4(h.zw, hC.zw + vec2(.5, 1));\n    \n}\n\n// Dot pattern.\nfloat dots(in vec2 p){\n    \n\tp = abs(fract(p) - .5);\n    \n    return length(p); // Circles.\n    \n    //return (p.x + p.y)/1.5 + .035; // Diamonds.\n    \n    //return max(p.x, p.y) + .03; // Squares.\n    \n    //return max(p.x*.866025 + p.y*.5, p.y) + .01; // Hexagons.\n    \n    //return min((p.x + p.y)*.7071, max(p.x, p.y)) + .08; // Stars.\n    \n    \n}\n\n\n// Distance field for the arcs. I think it's called poloidal rotation, or something like that.\nfloat dfPol(vec2 p){\n     \n    return length(p); // Circular arc.\n    \n    // There's no rule that says the arcs have to be rounded. Here's a hexagonal one.\n    //return hex(p);\n    \n    // Dodecahedron.\n    //return max(hex(p), hex(r2(3.14159/6.)*p));\n    \n    // Triangle.\n    //return max(abs(p.x)*.866025 - p.y, p.y);\n    \n}\n\n\n// Truchet pattern distance field.\nfloat df(vec2 p, float dir){\n     \n    // Weird UV coordinates. The first entry is the Truchet distance field itself,\n    // and the second is the polar angle of the arc pixel. The extra \".1\" is just a bit\n    // of mutational scaling, or something... I can't actually remember why it's there. :)\n    vec2 uv = vec2(p.x + .1, p.y);//*vec2(1, 1); // Scaling.\n    \n    // A checkered dot pattern. At present the pattern needs to have flip symmetry about\n    // the center, but I'm pretty sure regular textures could be applied with a few\n    // minor changes. Due to the triangular nature of the Truchet pattern, factors of \"3\"\n    // were necessary, but factors of \"1.5\" seemed to work too. Hence the \"4.5.\"\n    return min(dots(uv*4.5), dots(uv*4.5 + .5)) - .3;\n    \n}\n\n\n// Polar coordinate of the arc pixel.\nfloat getPolarCoord(vec2 q, float dir){\n    \n    // The actual animation. You perform that before polar conversion.\n    q = r2(iTime*dir)*q;\n    \n    // Polar angle.\n    const float aNum = 1.;\n    float a = atan(q.y, q.x);\n   \n    // Wrapping the polar angle.\n    return mod(a/3.14159, 2./aNum) - 1./aNum;\n   \n    \n}\n\nvoid mainImage(out vec4 fragColor, in vec2 fragCoord){\n\n    // I didn't feel like tayloring the antiasing to suit every resolution, which can get tiring, \n    // so I've put a range on it. Just for the record, I coded this for the 800 by 450 pixel canvas.\n    float res = clamp(iResolution.y, 300., 600.); \n    \n    // Aspect correct screen coordinates.\n\tvec2 u = (fragCoord - iResolution.xy*.5)/res;\n    \n    // Scaling and moving the screen coordinates.\n    vec2 sc = u*4. + s.yx*iTime/12.;\n    \n    // Converting the scaled and translated pixels to a hexagonal grid cell coordinate and\n    // a unique coordinate ID. The resultant vector contains everything you need to produce a\n    // pretty pattern, so what you do from here is up to you.\n    vec4 h = getHex(sc); // + s.yx*iTime/2.\n    \n    // Obtaining some offset values to do a bit of cubic shading. There are probably better ways\n    // to go about it, but it's quick and gets the job done.\n    vec4 h2 = getHex(sc - 1./s);\n    vec4 h3 = getHex(sc + 1./s);\n    \n    // Storing the hexagonal coordinates in \"p\" to save having to write \"h.xy\" everywhere.\n    vec2 p = h.xy;\n    \n    // The beauty of working with hexagonal centers is that the relative edge distance will simply \n    // be the value of the 2D isofield for a hexagon.\n    //\n    float eDist = hex(p); // Edge distance.\n    float cDist = dot(p, p); // Relative squared distance from the center.\n\n    \n    // Using the identifying coordinate - stored in \"h.zw,\" to produce a unique random number\n    // for the hexagonal grid cell.\n    float rnd = hash21(h.zw);\n    //float aRnd = sin(rnd*6.283 + iTime*1.5)*.5 + .5; // Animating the random number.\n    \n    #ifdef INTERLACING\n    // Random vec3 - used for some overlapping.\n    //vec3 lRnd = vec3(rnd*14.4 + .81, fract(rnd*21.3 + .97), fract(rnd*7.2 + .63));\n    vec3 lRnd = vec3(hash21(h.zw + .23), hash21(h.zw + .96), hash21(h.zw + .47));\n    #endif\n    \n    // It's possible to control the randomness to form some kind of repeat pattern.\n    //rnd = mod(h.z + h.w, 2.);\n    \n\n    // Redundant here, but I might need it later.\n    float dir = 1.;\n    \n\n    \n    // Storage vector.\n    vec2 q;\n    \n    \n    // If the grid cell's random ID is above a threshold, flip the Y-coordinates.\n    if(rnd>.5) p.y = -p.y;\n        \n    \n\n    // Determining the closest of the three arcs to the current point, the keeping a copy\n    // of the vector used to produce it. That way, you'll know just to render that particular\n    // decorated arc, lines, etc - instead of all three. \n    const float r = 1.;\n    const float th = .2; // Arc thickness.\n    \n    // Arc one.\n    q = p - vec2(0, r)/s;\n    vec3 da = vec3(q, dfPol(q));\n    \n    // Arc two. \"r2\" could be hardcoded, but this is a relatively cheap 2D example.\n    q = r2(3.14159*2./3.)*p - vec2(0, r)/s;\n    vec3 db = vec3(q, dfPol(q));\n\n     // Arc three. \n    q = r2(3.14159*4./3.)*p - vec2(0, r)/s;\n    vec3 dc = vec3(q, dfPol(q));\n    \n    // Compare distance fields, and return the vector used to produce the closest one.\n    vec3 q3 = da.z<db.z && da.z<dc.z? da : db.z<dc.z ? db : dc;\n    \n    \n    // TRUCHET PATTERN\n    //\n    // Set the poloidal arc radius: You can change the poloidal distance field in \n    // the \"dfPol\" function to a different curve shape, but you'll need to change\n    // the radius to one of the figures below.\n    //\n    q3.z -= .57735/2. + th/2.;  // Circular and dodecahedral arc/curves.\n    //q3.z -= .5/2. + th/2.;  // Hexagon curve.\n    //q3.z -= .7071/2. + th/2.;  // Triangle curve.\n    \n    q3.z = max(q3.z, -th - q3.z); // Chop out the smaller radius. The result is an arc.\n    \n    // Store the result in \"d\" - only to save writing \"q3.z\" everywhere.\n    float d = q3.z;\n    \n    // If you'd like to see the maze by itself.\n    #ifdef MAZE_ONLY\n    d += 1e5;\n    #endif\n    \n    // Truchet border.\n    float dBord = max(d - .015, -d);\n    \n\n    \n \n    \n    // MAZE BORDERS\n    // Producing the stright-line arc borders. Basically, we're rendering some hexagonal borders around\n    // the arcs. The result is the hexagonal maze surrounding the Truchet pattern.\n    q = q3.xy;\n    const float lnTh = .05;\n    q = abs(q);\n    \n    float arcBord = hex(q);\n    //float arcBord = length(q); // Change arc length to \".57735.\"\n    //float arcBord = max(hex(q), hex(r2(3.14159/6.)*q)); // Change arc length to \".57735.\"\n    \n    // Making the hexagonal arc.\n    float lnOuter = max(arcBord - .5, -(arcBord - .5 + lnTh)); //.57735\n    \n    \n    #ifdef INTERLACING\n    float ln = min(lnOuter, (q.y*.866025 + q.x*.5, q.x) - lnTh);\n    #else\n    float ln = min(lnOuter, arcBord - lnTh);\n    #endif\n    float lnBord = ln - .03; // Border lines to the maze border, if that makes any sense. :)\n     \n    \n   \n    \n    ///////\n    // The moving Truchet pattern. The polar coordinates consist of a wrapped angular coordinate,\n    // and the distance field itself.\n    float a = getPolarCoord(q3.xy, dir);\n    float d2 = df(vec2(q3.z, a), dir); \n    \n    // Smoothstepped Truchet mask.\n    float dMask = smoothstep(0., .015, d);\n    ///////\n    \n    // Producing the background with some subtle gradients.\n    vec3 bg =  mix(vec3(0, .4, .6), vec3(0, .3, .7), dot(sin(u*6. - cos(u*3.)), vec2(.4/2.)) + .4); \n    bg = mix(bg, bg.xzy, dot(sin(u*6. - cos(u*3.)), vec2(.4/2.)) + .4);\n    bg = mix(bg, bg.zxy, dot(sin(u*3. + cos(u*3.)), vec2(.1/2.)) + .1);\n   \n    #ifdef INTERLACING\n    // Putting in background cube lines for the interlaced version.\n    float hLines = smoothstep(0., .02, eDist - .5 + .02);\n    bg = mix(bg, vec3(0), smoothstep(0., .02, ln)*dMask*hLines);\n    #endif\n    \n    // Lines over the maze lines. Applying difference logic, depending on whether the \n    // pattern is interlaced or not.\n    const float tr = 1.;\n\n    float eDist2 = hex(h2.xy);\n    float hLines2 = smoothstep(0., .02, eDist2 - .5 + .02);\n    #ifdef INTERLACING\n    if(rnd>.5 && lRnd.x<.5) hLines2 *= smoothstep(0., .02, ln);\n    if(lRnd.x>.5) hLines2 *= dMask;\n    #else\n    if(rnd>.5) hLines2 *= smoothstep(0., .02, ln);\n    hLines2 *= dMask;\n    #endif\n    bg = mix(bg, vec3(0), hLines2*tr);\n    \n    float eDist3 = hex(h3.xy);\n    float hLines3 = smoothstep(0., .02, eDist3 - .5 + .02);\n    #ifdef INTERLACING\n    if(rnd<=.5 && lRnd.x>.5) hLines3 *= smoothstep(0., .02, ln);\n    if(lRnd.x>.5) hLines3 *= dMask;\n    #else\n    if(rnd<=.5) hLines3 *= smoothstep(0., .02, ln);\n    hLines3 *= dMask;\n    #endif\n    bg = mix(bg, vec3(0), hLines3*tr);\n\n\n    // Using the two off-centered hex coordinates to give the background a bit of highlighting.\n    float shade = max(1.25 - dot(h2.xy, h2.xy)*2., 0.);\n    shade = min(shade, max(dot(h3.xy, h3.xy)*3. + .25, 0.));\n    bg = mix(bg, vec3(0), (1.-shade)*.5); \n    \n    // I wanted to change the colors of everything at the last minute. It's pretty hacky, so\n    // when I'm feeling less lazy, I'll tidy it up. :)\n    vec3 dotCol = bg.zyx*vec3(1.5, .4, .4);\n    vec3 bCol = mix(bg.zyx, bg.yyy, .25);\n    bg = mix(bg.yyy, bg.zyx, .25);\n    \n\n    // Under the random threshold, and we draw the lines under the Truchet pattern.\n    #ifdef INTERLACING\n    if(lRnd.x>.5){\n       bg = mix(bg, vec3(0), (1. - smoothstep(0., .015, lnBord)));\n       bg = mix(bg, bCol, (1. - smoothstep(0., .015, ln))); \n       // Center lines.\n       bg = mix(bg, vec3(0), smoothstep(0., .02, eDist3 - .5 + .02)*tr);\n    }\n    #else\n    bg = mix(bg, vec3(0), (1. - smoothstep(0., .015, lnBord)));\n    bg = mix(bg, bCol, (1. - smoothstep(0., .015, ln)));\n    #endif\n\n   \n    \n    // Apply the Truchet shadow to the background.\n    bg = mix(bg, vec3(0), (1. - smoothstep(0., .07, d))*.5);\n    \n    \n    // Place the Truchet field to the background, with some additional shading to give it a \n    // slightly rounded, raised feel.\n    //vec3 col = mix(bg, vec3(1)*max(-d*3. + .7, 0.), (1. - dMask)*.65);\n    // Huttarl suggest slightly more shading on the snake-like pattern edges, so I added just a touch.\n    vec3 col = mix(bg, vec3(1)*max(-d*9. + .4, 0.), (1. - dMask)*.65);\n\n\n    \n    // Apply the moving dot pattern to the Truchet.\n    //dotCol = mix(dotCol, dotCol.xzy, dot(sin(u*3.14159*2. - cos(u.yx*3.14159*2.)*3.14159), vec2(.25)) + .5);\n    col = mix(col, vec3(0), (1. - dMask)*(1. - smoothstep(0., .02, d2)));\n    col = mix(col, dotCol, (1. - dMask)*(1. - smoothstep(0., .02, d2 + .125)));\n    \n    // Truchet border.\n    col = mix(col, vec3(0), 1. - smoothstep(0., .015, dBord));\n    \n    #ifdef INTERLACING\n    // Over the random threshold, and we draw the lines over the Truchet.\n    if(lRnd.x<=.5){\n        col = mix(col, vec3(0), (1. - smoothstep(0., .015, lnBord)));\n        col = mix(col, bCol, (1. - smoothstep(0., .015, ln)));  \n        // Center lines.\n        col = mix(col, vec3(0), smoothstep(0., .02, eDist2 - .5 + .02)*tr);\n    }\n    #endif\n\n        \n    \n    \n    // Using the offset hex values for a bit of fake 3D highlighting.\n    //if(rnd>.5) h3.y = -h3.y; // All raised edges. Spoils the mild 3D illusion.\n    #ifdef INTERLACING\n    float trSn = max(dMask, 1. - smoothstep(0., .015, lnBord))*.75 + .25;\n    #else\n    float trSn = dMask*.75 + .25;\n    #endif\n    col = mix(col, vec3(0), trSn*(1. - hex(s/2.+h2.xy)));\n    col = mix(col, vec3(0), trSn*(1. - hex(s/2.-h3.xy)));\n \n    \n    // Using the edge distance to produce some repeat contour lines. Standard stuff.\n    //if (rnd>.5) h.xy = -h.yx;\n    //float cont = clamp(cos(hex(h.xy)*6.283*12.)*1.5 + 1.25, 0., 1.);\n    //col = mix(col, vec3(0), (1. - smoothstep(0., .015, ln))*(smoothstep(0., .015, d))*(1.-cont)*.5);\n    \n    \n    // Very basic hatch line effect.\n    float gr = dot(col, vec3(.299, .587, .114));\n    float hatch = (gr<.45)? clamp(sin((sc.x - sc.y)*3.14159*40.)*2. + 1.5, 0., 1.) : 1.;\n    float hatch2 = (gr<.25)? clamp(sin((sc.x + sc.y)*3.14159*40.)*2. + 1.5, 0., 1.) : 1.;\n\n    col *= min(hatch, hatch2)*.5 + .5;    \n    col *= clamp(sin((sc.x - sc.y)*3.14159*80.)*1.5 + .75, 0., 1.)*.25 + 1.;  \n    \n \n    // Subtle vignette.\n    u = fragCoord/iResolution.xy;\n    col *= pow(16.*u.x*u.y*(1. - u.x)*(1. - u.y) , .125) + .25;\n    // Colored variation.\n    //col = mix(pow(min(vec3(1.5, 1, 1)*col, 1.), vec3(1, 3, 16)), col, \n            //pow(16.*u.x*u.y*(1. - u.x)*(1. - u.y) , .25)*.75 + .25);    \n    \n\n    \n    // Rough gamma correction.    \n\tfragColor = vec4(sqrt(max(col, 0.)), 1);\n    \n}",
                "description": "",
                "inputs": [],
                "name": "Image",
                "outputs": [
                    {
                        "channel": 0,
                        "id": 37
                    }
                ],
                "type": "image"
            }
        ],
        "ver": "0.1"
    }
}