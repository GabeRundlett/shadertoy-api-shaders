{
    "Shader": {
        "info": {
            "date": "1526026806",
            "description": "Try of using more realistic sand waves...\nOriginal 'Desert Sand' function 'sandL()' replaced with 'Dune Stripes' (refs see code).\nAdded: use your mouse to look around.\nMissing a sandstorm like at 'Skin peeler'.\n\n",
            "flags": 64,
            "hasliked": 0,
            "id": "XdcBW4",
            "likes": 25,
            "name": "Desert Sand+Dune Stripes",
            "published": 3,
            "tags": [
                "3d",
                "noise",
                "raymarcher",
                "stripes",
                "sand",
                "desert",
                "dune"
            ],
            "usePreview": 1,
            "username": "tholzer",
            "viewed": 2366
        },
        "renderpass": [
            {
                "code": "/*\tDesertSand + DuneStripes        2018-05-11\n\t------------------------\n\n    Original   'Desert Sand'  https://www.shadertoy.com/view/ld3BzM\n    Now using  'Dune Stripes' https://www.shadertoy.com/view/MsG3Wm\n    Sound from 'Skin peeler'  https://www.shadertoy.com/view/XtfSWX\n\n    Tags: 3d, desert, dune, sand, stripes, raymarcher, noise\n*/\n\n#define SPEED 0.05\n#define DUNE_SIZE 3.6\n\n/*\n\tDesert Sand\n\t-----------\n\n\tSand, and more sand -- Monotony, at it's finest. :) I've visited and flown over many sandy \n\tregions in my time, and I can say unequivocably that this particular scene doesn't remotely  \n\tresemble the real thing. :D \n\n\tHaving said that, there's something that I really like about minimal artificial dune scenes.  \n\tThey're somewhat of a visual programmer cliche -- I think that's partly due to the fact that \n\tthey have a decent \"aesthetic appeal to algorithmic complexity\" ratio.\n\t\n\tFor the sand dunes, rather than deal with dune physics, I used a pretty standard smoothstep \n\tnoise layer formula. I wouldn't say that dune physics is particularly difficult, but I'd \n\trather leave that to people like Dr2. :) Besides, with this example, I wanted to save some\n\tcycles and focus on the sand texture generation.\n\n\tThere are so many different ways to create wavy sand patterns. Some are expensive -- using\n\tthings like gradient erosion -- and some are cheap. Not suprisingly, the expensive methods \n\ttend to look better. I bump mapped the sand layer to give myself a few extra cycles to play \n\twith, but I still had to keep things relatively simple.\n\n\tThe pattern you see is a mixture of a simple trick I've seen around and some of my own\n\tadjustments. Without going into detail, the idea is to create a layer of repeat rounded \n\tgradient lines, and another rotated at slight angle, then perturb them slightly and mix \n\ttogether using an underlying noise layer. It's simple, but reasonably effective. Anyway, \n\tI've explained in more detail below.\n\t\n\n\tRelated examples:\n\n\tIt won Breakpoint way back in 2009. Ten people on Pouet gave it the thumbs down.\n    I hope they put their work up on Shadertoy, because it must be insanely good. :D\n      Elevated  -  IQ  -  https://www.shadertoy.com/view/MdX3Rr\n \n\tOne of my favorite simple coloring jobs.\n      Skin Peeler  -  Dave Hoskins  -  https://www.shadertoy.com/view/XtfSWX\n\n    Based on one of my all time favorites:\n      Xyptonjtroz  -  Nimitz  -  https://www.shadertoy.com/view/4ts3z2\n*/\n\n// The far plane. I'd like this to be larger, but the extra iterations required to render the \n// additional scenery starts to slow things down on my slower machine.\n#define FAR 100.\n\n\n// 2x2 matrix rotation. Angle vector, courtesy of Fabrice.\nmat2 rot2( float th ){ vec2 a = sin(vec2(1.5707963, 0) + th); return mat2(a, -a.y, a.x); }\n\n\n// 1x1 and 3x1 hash functions.\nfloat hash( float n ){ return fract(cos(n)*45758.5453); }\nfloat hash( vec3 p  ){ return fract(sin(dot(p, vec3(21.71, 157.97, 113.43)))*45758.5453); }\n\n\n// IQ's smooth minium function. \nfloat smin(float a, float b , float s)\n{\n    float h = clamp( 0.5 + 0.5*(b-a)/s, 0. , 1.);\n    return mix(b, a, h) - h*(1.0-h)*s;\n}\n\n// Smooth maximum, based on IQ's smooth minimum.\nfloat smax(float a, float b, float s)\n{\n    float h = clamp( 0.5 + 0.5*(a-b)/s, 0., 1.);\n    return mix(b, a, h) + h*(1.0-h)*s;\n}\n\n// 2x2 matrix rotation. Note the absence of \"cos.\" It's there, but in disguise, and comes courtesy\n// of Fabrice Neyret's \"ouside the box\" thinking. :)\n//mat2 rot2( float a ){ vec2 v = sin(vec2(1.570796, 0) + a);\treturn mat2(v, -v.y, v.x); }\n\n/*\n// Cheap and nasty 2D smooth noise function with inbuilt hash function - based on IQ's \n// original. Very trimmed down. In fact, I probably went a little overboard. I think it \n// might also degrade with large time values. I'll swap it for something more robust later.\nfloat n2D(vec2 p) {\n\n\tvec2 i = floor(p); p -= i; p *= p*(3. - p*2.);  \n    \n\treturn dot(mat2(fract(sin(vec4(0, 1, 113, 114) + dot(i, vec2(1, 113)))*43758.5453))*\n                vec2(1. - p.y, p.y), vec2(1. - p.x, p.x) );\n\n}\n*/\n\n// Fabrice's consice, 2D rotation formula.\nmat2 r2(float th){ vec2 a = sin(vec2(1.5707963, 0) + th); return mat2(a, -a.y, a.x); }\n\n/*\n// Dave's hash function. More reliable with large values, but will still eventually break down.\n//\n// Hash without Sine\n// Creative Commons Attribution-ShareAlike 4.0 International Public License\n// Created by David Hoskins.\n// vec2 to vec2.\nvec2 hash22(vec2 p){\n\n\tvec3 p3 = fract(vec3(p.xyx) * vec3(.1031, .1030, .0973));\n    p3 += dot(p3, p3.yzx + 19.19);\n    p = fract((p3.xx + p3.yz)*p3.zy)*2. - 1.;\n    return p;\n    \n    \n    // Note the \"mod\" call. Slower, but ensures accuracy with large time values.\n    //mat2  m = r2(mod(iTime, 6.2831853)); \n\t//p.xy = m * p.xy;//rotate gradient vector\n  \t//return p;\n}\n*/\n\n\n// Standard 2x2 hash algorithm.\nvec2 hash22(vec2 p) \n{\n  // Faster, but probaly doesn't disperse things as nicely as other methods.\n  float n = sin(dot(p, vec2(113, 1)));\n  p = fract(vec2(2097152, 262144)*n)*2. - 1.;\n  return p;\n}\n\n// Gradient noise. Ken Perlin came up with it, or a version of it. Either way, this is\n// based on IQ's implementation. It's a pretty simple process: Break space into squares, \n// attach random 2D vectors to each of the square's four vertices, then smoothly \n// interpolate the space between them.\nfloat gradN2D(in vec2 f)\n{\n   // Used as shorthand to write things like vec3(1, 0, 1) in the short form, e.yxy. \n   const vec2 e = vec2(0, 1);\n   \n    // Set up the cubic grid.\n    // Integer value - unique to each cube, and used as an ID to generate random vectors for the\n    // cube vertiies. Note that vertices shared among the cubes have the save random vectors attributed\n    // to them.\n    vec2 p = floor(f);\n    f -= p; // Fractional position within the cube.\n    \n\n    // Smoothing - for smooth interpolation. Use the last line see the difference.\n    //vec2 w = f*f*f*(f*(f*6.-15.)+10.); // Quintic smoothing. Slower and more squarish, but derivatives are smooth too.\n    vec2 w = f*f*(3. - 2.*f); // Cubic smoothing. \n    //vec2 w = f*f*f; w = ( 7. + (w - 7. ) * f ) * w; // Super smooth, but less practical.\n    //vec2 w = .5 - .5*cos(f*3.14159); // Cosinusoidal smoothing.\n    //vec2 w = f; // No smoothing. Gives a blocky appearance.\n    \n    // Smoothly interpolating between the four verticies of the square. Due to the shared vertices between\n    // grid squares, the result is blending of random values throughout the 2D space. By the way, the \"dot\" \n    // operation makes most sense visually, but isn't the only metric possible.\n    float c = mix(mix(dot(hash22(p + e.xx), f - e.xx), dot(hash22(p + e.yx), f - e.yx), w.x),\n                  mix(dot(hash22(p + e.xy), f - e.xy), dot(hash22(p + e.yy), f - e.yy), w.x), w.y);\n    \n    // Taking the final result, and converting it to the zero to one range.\n    return c*.5 + .5; // Range: [0, 1].\n}\n\n// Gradient noise fBm (Fractional Brownian Motion ?)\nfloat fBm(in vec2 p)\n{\n    return gradN2D(p)*.57 + gradN2D(p*2.)*.28 + gradN2D(p*4.)*.15;\n}\n\n\n// Cheap and nasty 2D smooth noise function with inbuilt hash function - based on IQ's \n// original. Very trimmed down. In fact, I probably went a little overboard. I think it \n// might also degrade with large time values. I'll swap it for something more robust later.\nfloat n2D(vec2 p) \n{\n\tvec2 i = floor(p); p -= i; \n    //p *= p*p*(p*(p*6. - 15.) + 10.);\n    p *= p*(3. - p*2.);  \n    \n\treturn dot(mat2( fract(sin(vec4(0, 1, 113, 114) + dot(i, vec2(1, 113)))*43758.5453) )*\n                vec2(1. - p.y, p.y), vec2(1. - p.x, p.x) );\n}\n\n\n// Repeat gradient lines. How you produce these depends on the effect you're after. I've used a smoothed\n// triangle gradient mixed with a custom smoothed gradient to effect a little sharpness. It was produced\n// by trial and error. If you're not sure what it does, just call it individually, and you'll see.\nfloat grad(float x, float offs)\n{\n    // Repeat triangle wave. The tau factor and \".25\" factor aren't necessary, but I wanted its frequency\n    // to overlap a sine function.\n    x = abs(fract(x/6.283 + offs - .25) - .5)*2.;\n    \n    float x2 = clamp(x*x*(-1. + 2.*x), 0., 1.); // Customed smoothed, peaky triangle wave.\n    //x *= x*x*(x*(x*6. - 15.) + 10.); // Extra smooth.\n    x = smoothstep(0., 1., x); // Basic smoothing - Equivalent to: x*x*(3. - 2.*x).\n    return mix(x, x2, .15);\n    \n/*    \n    // Repeat sine gradient.\n    float s = sin(x + 6.283*offs + 0.);\n    return s*.5 + .5;\n    // Sine mixed with an absolute sine wave.\n    //float sa = sin((x +  6.283*offs)/2.);\n    //return mix(s*.5 + .5, 1. - abs(sa), .5);\n    \n*/\n}\n\n\n// replacement of 'sandL'\n//---------------------------------------------------------\n// from Desert stripes   https://www.shadertoy.com/view/MsG3Wm\n//---------------------------------------------------------\nvec2 coord;\n\nvec2 hash( vec2 p )                       // rand in [-1,1]\n{\n  p = vec2(dot(p,vec2(127.1,311.7)),\n           dot(p,vec2(269.5,183.3)));\n  return -1. + 2.*fract(sin(p+20.)*53758.5453123);\n}\n//---------------------------------------------------------\n// 2d noise functions from https://www.shadertoy.com/view/XslGRr\nfloat noise( in int i, in int j )\n{\n  vec2 n = vec2(i,j); \n  vec2 p = floor(n);\n  vec2 f = fract(n);\n  f = f*f*(3.0-2.0*f);\n  vec2 uv = (p+vec2(37.0,17.0)) + f;\n  vec2 rg = hash( uv/256.0 ).yx;\n  return 0.5*mix( rg.x, rg.y, 0.5 );\n}\n\n//---------------------------------------------------------\n#define NUM 100     // number or gabor blobs\n#define SIZE 0.25   // size of gabor blobs\n                       // freq tuned by mouse.x\n\nfloat DuneStripes (vec2 uv, float d, float freq, float time)\n{\n  float hv = -0.5;\n  for (int i = 0; i < NUM; i++) \n  {\n    vec2 pos = vec2(noise(i,0), noise(i,1));\n    vec2 dir = (1.+d)*vec2(noise(i,2),noise(i,3)) - d;\n    hv += SIZE * sin(dot(uv-pos, freq*dir) * 6. + time);\n  }\n  return hv;\n}\n//---------------------------------------------------------\n#define GB_NUM 100     // number or gabor blobs\n#define GB_SIZE 0.01   // size of gabor blobs\n\nfloat DuneStripes (vec2 p, float d, float freq)\n{\n  float hv = -0.5;\n  float time = iTime;\n  p.x *= 0.5;\n  for (int i = 0; i < GB_NUM; i++) \n  {\n    vec2 pos = vec2(noise(i,0), noise(i,1));\n    vec2 dir = (0.5+d)*vec2(noise(i,2),noise(i,3)) - d;\n    hv += GB_SIZE + 0.05 * sin(dot(p, freq*dir) * 6. + time);\n  }\n  return hv;\n}\n\n// replacement with DuneStripes\nfloat sandL(vec2 p)\n{\n  return DuneStripes(p, 6., 2.);\n}\n//---------------------------------------------------------\n// One sand function layer... which is comprised of two mixed, rotated layers of repeat gradients lines.\nfloat xsandL(vec2 p){\n    \n    // Layer one. \n    vec2 q = rot2(3.14159/18.)*p; // Rotate the layer, but not too much.\n    q.y += (gradN2D(q*18.) - .5)*.05; // Perturb the lines to make them look wavy.\n    float grad1 = grad(q.y*80., 0.); // Repeat gradient lines.\n   \n    q = rot2(-3.14159/20.)*p; // Rotate the layer back the other way, but not too much.\n    q.y += (gradN2D(q*12.) - .5)*.05; // Perturb the lines to make them look wavy.\n    float grad2 = grad(q.y*80., .5); // Repeat gradient lines.\n      \n    \n    // Mix the two layers above with an underlying 2D function. The function you choose is up to you,\n    // but it's customary to use noise functions. However, in this case, I used a transcendental \n    // combination, because I like the way it looked better.\n    // \n    // I feel that rotating the underlying mixing layers adds a little variety. Although, it's not\n    // completely necessary.\n    q = rot2(3.14159/4.)*p;\n    //float c = mix(grad1, grad2, smoothstep(.1, .9, n2D(q*vec2(8))));//smoothstep(.2, .8, n2D(q*8.))\n    //float c = mix(grad1, grad2, n2D(q*vec2(6)));//smoothstep(.2, .8, n2D(q*8.))\n    //float c = mix(grad1, grad2, dot(sin(q*12. - cos(q.yx*12.)), vec2(.25)) + .5);//smoothstep(.2, .8, n2D(q*8.))\n    \n    // The mixes above will work, but I wanted the use a suble screen blend of grad1 and grad2.\n    float a2 = dot(sin(q*12. - cos(q.yx*12.)), vec2(.25)) + .5;\n    float a1 = 1. - a2;\n    \n    grad1 *= a1;\n    grad2 *= a2;\n    \n    // Screen blend.\n    float c = 1. - (1. - grad1)*(1. - grad2);\n    \n    // Smooth max\\min\n    //float c = smax(grad1, grad2, .5);\n\n    return c;\n}\n\n// A global value to record the distance from the camera to the hit point. It's used to tone\n// down the sand height values that are further away. If you don't do this, really bad\n// Moire artifacts will arise. By the way, you should always avoid globals, if you can, but\n// I didn't want to pass an extra variable through a bunch of different functions.\nfloat gT;\n\nfloat sand(vec2 p)\n{\n    // Rotating by 45 degrees. I thought it looked a little better this way. Not sure why.\n    // I've also zoomed in by a factor of 4.\n    p = vec2(p.y - p.x, p.x + p.y)*.7071/4.;\n    \n    // Sand layer 1.\n    float c1 = sandL(p);\n    \n    // Second layer.\n    // Rotate, then increase the frequency -- The latter is optional.\n    vec2 q = rot2(3.14159/12.)*p;\n    float c2 = sandL(q*1.25);\n    \n    // Mix the two layers with some underlying gradient noise.\n //   c1 = mix(c1, c2, smoothstep(.1, .9, gradN2D(p*vec2(4))));\n    \n/*   \n\t// Optional screen blending of the layers. I preferred the mix method above.\n    float a2 = gradN2D(p*vec2(4));\n    float a1 = 1. - a2;\n    \n    // Screen blend.\n    c1 = 1. - (1. - c1*a1)*(1. - c2*a2);\n*/    \n    \n    // Extra grit. Not really necessary.\n    //c1 = .7 + fBm(p*128.)*.3;\n    \n    // A surprizingly simple and efficient hack to get rid of the super annoying Moire pattern \n    // formed in the distance. Simply lessen the value when it's further away. Most people would\n    // figure this out pretty quickly, but it took me far too long before it hit me. :)\n    return c1/(1. + gT*gT*.015);\n}\n\n/////////\n// The path is a 2D sinusoid that varies over time, which depends upon the frequencies and amplitudes.\nvec2 path(in float z)\n{ \n    return vec2(4.*sin(z * .1), 0);\n}\n\n// The standard way to produce \"cheap\" dunes is to apply a triangle function to individual\n// noise layers varying in amplitude and frequency. However, I needed something more subtle\n// and rounder, so I've only applied a triangle function to the middle layer.\n// \n// Here's an example using a more standard routine that's worth taking a look at:\n//\n// desert - wachel\n// https://www.shadertoy.com/view/ltcGDl\nfloat surfFunc( in vec3 p)\n{\n    p /= 2.5;\n    \n    // Large base ampltude with lower frequency.\n    float layer1 = n2D(p.xz*.2)*2. - .5; // Linear-like discontinuity - Gives an edge look.\n    layer1 = smoothstep(0., 1.05, layer1); // Smoothing the sharp edge.\n\n    // Medium amplitude with medium frequency. \n    float layer2 = n2D(p.xz*.275);\n    layer2 = 1. - abs(layer2 - .5)*2.; // Triangle function, to give the dune edge look.\n    layer2 = smoothstep(.2, 1., layer2*layer2); // Smoothing the sharp edge.\n    \n    // Smaller, higher frequency layer.\n\tfloat layer3 = n2D(p.xz*.5*3.);\n\n     // Combining layers fBm style. Ie; Amplitudes inversely proportional to frequency.\n    float res = layer1*.7 + layer2*.25 + layer3*.05;\n    //float res = 1. - (1. - layer1*.7)*(1. - layer2*.25)*(1. - layer3*.05); // Screen \n    //float res = layer1*.75 + layer2*.25;\n\n    return res;\n    \n}\n\n\n// A similar -- trimmed down and smoothed out -- version of function above, for camera path usage.\nfloat camSurfFunc( in vec3 p)\n{\n    p /= 2.5;\n    \n    // Large base ampltude with lower frequency.\n    float layer1 = n2D(p.xz*.2)*2. - .5; // Linear-like discontinuity - Gives an edge look.\n    layer1 = smoothstep(0., 1.05, layer1); // Smoothing the sharp edge.\n\n    // Medium amplitude with medium frequency. \n    float layer2 = n2D(p.xz*.275);\n    layer2 = 1. - abs(layer2 - .5)*2.; // Triangle function, to give the dune edge look.\n    layer2 = smoothstep(.2, 1., layer2*layer2); // Smoothing the sharp edge.\n\n     // Combining layers fBm style. Ie; Amplitudes inversely proportional to frequency.\n    float res = (layer1*.7 + layer2*.25)/.95;\n    //float res = 1. - (1. - layer1*.75)*(1. - layer2*.25); // Screen \n\n    return res;\n}\n\n\n\n// The desert scene. Adding a heightmap to an XZ plane. Not a complicated distance function. :)\nfloat map(vec3 p){\n    \n\t// Height map to perturb the flat plane. On a side note, I'll usually keep the\n    // surface function within a zero to one range, which means I can use it later\n    // for a bit of shading, etc. Of course, I could cut things down a bit, but at\n    // the expense of confusion elsewhere... if that makes any sense. :)\n    float sf = surfFunc(p);\n\n    // Add the height map to the plane.\n    return p.y + (.5-sf)*DUNE_SIZE; \n}\n\n\n\n// Basic raymarcher.\nfloat trace(in vec3 ro, in vec3 rd){\n\n    float t = 0., h;\n    \n    for(int i=0; i<96; i++){\n    \n        h = map(ro + rd*t);\n        // Note the \"t*b + a\" addition. Basically, we're putting less emphasis on accuracy, as\n        // \"t\" increases. It's a cheap trick that works in most situations... Not all, though.\n        if(abs(h)<0.001*(t*.125 + 1.) || t>FAR) break; // Alternative: 0.001*max(t*.25, 1.), etc.\n        \n        t += h; \n    }\n\n    return min(t, FAR);\n}\n\n/*\n// Tetrahedral normal - courtesy of IQ. I'm in saving mode, so the two \"map\" calls saved make\n// a difference. Also because of the random nature of the scene, the tetrahedral normal has the \n// same aesthetic effect as the regular - but more expensive - one, so it's an easy decision.\nvec3 normal(in vec3 p)\n{  \n    vec2 e = vec2(-1., 1.)*0.001;   \n\treturn normalize(e.yxx*map(p + e.yxx) + e.xxy*map(p + e.xxy) + \n\t\t\t\t\t e.xyx*map(p + e.xyx) + e.yyy*map(p + e.yyy) );   \n}\n*/\n\n \n// Standard normal function. It's not as fast as the tetrahedral calculation, but more symmetrical.\nvec3 normal(in vec3 p, float ef) {\n\tvec2 e = vec2(0.001*ef, 0);\n\treturn normalize(vec3(map(p + e.xyy) - map(p - e.xyy), map(p + e.yxy) - map(p - e.yxy),\tmap(p + e.yyx) - map(p - e.yyx)));\n}\n\n/*\n// Tri-Planar blending function. Based on an old Nvidia writeup:\n// GPU Gems 3 - Ryan Geiss: https://developer.nvidia.com/gpugems/GPUGems3/gpugems3_ch01.html\nvec3 tex3D(sampler2D t, in vec3 p, in vec3 n ){\n    \n    n = max(abs(n) - .2, 0.001);\n    n /= dot(n, vec3(1));\n\tvec3 tx = texture(t, p.yz).xyz;\n    vec3 ty = texture(t, fract(p.zx)).xyz;\n    vec3 tz = texture(t, p.xy).xyz;\n\n    // Textures are stored in sRGB (I think), so you have to convert them to linear space \n    // (squaring is a rough approximation) prior to working with them... or something like that. :)\n    // Once the final color value is gamma corrected, you should see correct looking colors.\n    return (tx*tx*n.x + ty*ty*n.y + tz*tz*n.z);\n    \n}\n\n\n// Texture bump mapping. Four tri-planar lookups, or 12 texture lookups in total.\nvec3 doBumpMap( sampler2D tx, in vec3 p, in vec3 n, float bf){\n   \n    const vec2 e = vec2(0.001, 0);\n    \n    // Three gradient vectors rolled into a matrix, constructed with offset greyscale texture values.    \n    mat3 m = mat3( tex3D(tx, p - e.xyy, n), tex3D(tx, p - e.yxy, n), tex3D(tx, p - e.yyx, n));\n    \n    vec3 g = vec3(0.299, 0.587, 0.114)*m; // Converting to greyscale.\n    g = (g - dot(tex3D(tx,  p , n), vec3(0.299, 0.587, 0.114)) )/e.x; g -= n*dot(n, g);\n                      \n    return normalize( n + g*bf ); // Bumped normal. \"bf\" - bump factor.\n\t\n}\n*/\n\n// Compact, self-contained version of IQ's 3D value noise function. I have a transparent noise\n// example that explains it, if you require it.\nfloat n3D(in vec3 p){\n    \n\tconst vec3 s = vec3(113, 157, 1);\n\tvec3 ip = floor(p); p -= ip; \n    vec4 h = vec4(0., s.yz, s.y + s.z) + dot(ip, s);\n    p = p*p*(3. - 2.*p); //p *= p*p*(p*(p * 6. - 15.) + 10.);\n    h = mix(fract(sin(h)*43758.5453), fract(sin(h + s.x)*43758.5453), p.x);\n    h.xy = mix(h.xz, h.yw, p.y);\n    return mix(h.x, h.y, p.z); // Range: [0, 1].\n}\n\n\n// 3D noise fBm.\nfloat fBm(in vec3 p){\n    \n    return n3D(p)*.57 + n3D(p*2.)*.28 + n3D(p*4.)*.15;\n    \n}\n\n\n// Surface bump function..\nfloat bumpSurf3D( in vec3 p){\n    \n    // Obtaining some terrain samples in order to produce a gradient\n    // with which to distort the sand. Basically, it'll make it look\n    // like the underlying terrain it effecting the sand. The downside\n    // is the three extra taps per bump tap... Ouch. :) Actually, it's\n    // not that bad, but I might attempt to come up with a better way.\n    float n = surfFunc(p);\n    vec3 px = p + vec3(.001, 0, 0);\n    float nx = surfFunc(px);\n    vec3 pz = p + vec3(0, 0, .001);\n    float nz = surfFunc(pz);\n    \n    // The wavy sand, that have been perturbed by the underlying terrain.\n    return sand(p.xz + vec2(n - nx, n - nz)/.001*1.);\n\n}\n\n// Standard function-based bump mapping function.\nvec3 doBumpMap(in vec3 p, in vec3 nor, float bumpfactor){\n    \n    const vec2 e = vec2(0.001, 0);\n    float ref = bumpSurf3D(p);                 \n    vec3 grad = (vec3(bumpSurf3D(p - e.xyy),\n                      bumpSurf3D(p - e.yxy),\n                      bumpSurf3D(p - e.yyx) )-ref)/e.x;                     \n          \n    grad -= nor*dot(nor, grad);          \n                      \n    return normalize( nor + grad*bumpfactor );\n\t\n}\n\n// Cheap shadows are the bain of my raymarching existence, since trying to alleviate artifacts is an excercise in\n// futility. In fact, I'd almost say, shadowing - in a setting like this - with limited  iterations is impossible... \n// However, I'd be very grateful if someone could prove me wrong. :)\nfloat softShadow(vec3 ro, vec3 lp, float k, float t){\n\n    // More would be nicer. More is always nicer, but not really affordable.\n    const int maxIterationsShad = 24; \n    \n    vec3 rd = lp - ro; // Unnormalized direction ray.\n\n    float shade = 1.;\n    float dist = 0.001*(t*.125 + 1.);  // Coincides with the hit condition in the \"trace\" function.  \n    float end = max(length(rd), 0.0001);\n    //float stepDist = end/float(maxIterationsShad);\n    rd /= end;\n\n    // Max shadow iterations - More iterations make nicer shadows, but slow things down. Obviously, the lowest \n    // number to give a decent shadow is the best one to choose. \n    for (int i=0; i<maxIterationsShad; i++){\n\n         \n        float h = map(ro + rd*dist);\n        shade = min(shade, k*h/dist);\n        //shade = min(shade, smoothstep(0.0, 1.0, k*h/dist)); // Subtle difference. Thanks to IQ for this tidbit.\n        // So many options here, and none are perfect: dist += min(h, .2), dist += clamp(h, .01, stepDist), etc.\n        h = clamp(h, .1, .5); // max(h, .02);//\n        dist += h;\n\n        \n        // Early exits from accumulative distance function calls tend to be a good thing.\n        if (shade<0.001 || dist > end) break; \n    }\n\n    // I've added a constant to the final shade value, which lightens the shadow a bit. It's a preference thing. \n    // Really dark shadows look too brutal to me. Sometimes, I'll add AO also, just for kicks. :)\n    return min(max(shade, 0.) + .05, 1.); \n}\n\n\n\n// I keep a collection of occlusion routines... OK, that sounded really nerdy. :)\n// Anyway, I like this one. I'm assuming it's based on IQ's original.\nfloat calcAO(in vec3 p, in vec3 n)\n{\n\tfloat ao = 0.0, l;\n    const float maxDist = 4.;\n\tconst float nbIte = 5.;\n\t//const float falloff = .9;\n    for( float i=1.; i< nbIte+.5; i++ ){\n    \n        l = (i + .0)*.5/nbIte*maxDist;        \n        ao += (l - map( p + n*l )); // / pow(1.+l, falloff);\n    }\n\t\n    return clamp(1.- ao/nbIte, 0., 1.);\n}\n\n\n\n// Standard sky routine. Gradient with sun and overhead cloud plane. I debated over whether to put more \n// effort in, but the dust is there and I'm saving cycles. I put sun flares in, but I wasn't feeling it,\n// so took them out again. I might tweak them later, and see if I can make them work with the scene.\nvec3 getSky(vec3 ro, vec3 rd, vec3 ld){ \n    \n    // Sky color gradients.\n    vec3 col = vec3(.8, .7, .5), col2 = vec3(.4, .6, .9);\n    \n    //return mix(col, col2, pow(max(rd.y*.5 + .9, 0.), 5.));  // Probably a little too simplistic. :)\n     \n    // Mix the gradients using the Y value of the unit direction ray. \n    vec3 sky = mix(col, col2, pow(max(rd.y + .15, 0.), .5));\n    sky *= vec3(.7, 1, 1.3)*vec3(1.2, 1, .9); // Adding some extra vibrancy.\n     \n    float sun = clamp(dot(ld, rd), 0.0, 1.0);\n    sky += vec3(1, .7, .4)*vec3(pow(sun, 16.))*.2; // Sun flare, of sorts.\n    sun = pow(sun, 32.); // Not sure how well GPUs handle really high powers, so I'm doing it in two steps.\n    sky += vec3(1, .9, .6)*vec3(pow(sun, 32.))*.35; // Sun.\n    \n     // Subtle, fake sky curvature.\n    rd.z *= 1. + length(rd.xy)*.15;\n    rd = normalize(rd);\n   \n    // A simple way to place some clouds on a distant plane above the terrain -- Based on something IQ uses.\n    const float SC = 1e5;\n    float t = (SC - ro.y - .15)/(rd.y + .15); // Trace out to a distant XZ plane.\n    vec2 uv = (ro + t*rd).xz; // UV coordinates.\n    \n    // Mix the sky with the clouds, whilst fading out a little toward the horizon (The rd.y bit).\n\tif(t>0.) sky =  mix(sky, vec3(2), smoothstep(.45, 1., fBm(1.5*uv/SC))*\n                        smoothstep(.45, .55, rd.y*.5 + .5)*.4);\n    \n    // Return the sky color.\n    return sky;\n}\n\n\n// More concise, self contained version of IQ's original 3D noise function.\nfloat noise3D(in vec3 p){\n    \n    // Just some random figures, analogous to stride. You can change this, if you want.\n\tconst vec3 s = vec3(113, 157, 1);\n\t\n\tvec3 ip = floor(p); // Unique unit cell ID.\n    \n    // Setting up the stride vector for randomization and interpolation, kind of. \n    // All kinds of shortcuts are taken here. Refer to IQ's original formula.\n    vec4 h = vec4(0., s.yz, s.y + s.z) + dot(ip, s);\n    \n\tp -= ip; // Cell's fractional component.\n\t\n    // A bit of cubic smoothing, to give the noise that rounded look.\n    p = p*p*(3. - 2.*p);\n    \n    // Standard 3D noise stuff. Retrieving 8 random scalar values for each cube corner,\n    // then interpolating along X. There are countless ways to randomize, but this is\n    // the way most are familar with: fract(sin(x)*largeNumber).\n    h = mix(fract(sin(h)*43758.5453), fract(sin(h + s.x)*43758.5453), p.x);\n\t\n    // Interpolating along Y.\n    h.xy = mix(h.xz, h.yw, p.y);\n    \n    // Interpolating along Z, and returning the 3D noise value.\n    return mix(h.x, h.y, p.z); // Range: [0, 1].\n\t\n}\n\n/////\n// Code block to produce some layers of smokey haze. Not sophisticated at all.\n// If you'd like to see a much more sophisticated version, refer to Nitmitz's\n// Xyptonjtroz example. Incidently, I wrote this off the top of my head, but\n// I did have that example in mind when writing this.\n\n// Hash to return a scalar value from a 3D vector.\nfloat hash31(vec3 p){ return fract(sin(dot(p, vec3(127.1, 311.7, 74.7)))*43758.5453); }\n\n// Four layers of cheap cell tile noise to produce some subtle mist.\n// Start at the ray origin, then take four samples of noise between it\n// and the surface point. Apply some very simplistic lighting along the \n// way. It's not particularly well thought out, but it doesn't have to be.\nfloat getMist(in vec3 ro, in vec3 rd, in vec3 lp, in float t){\n\n    float mist = 0.;\n    \n    //ro -= vec3(0, 0, iTime*3.);\n    \n    float t0 = 0.;\n    \n    for (int i = 0; i<24; i++){\n        \n        // If we reach the surface, don't accumulate any more values.\n        if (t0>t) break; \n        \n        // Lighting. Technically, a lot of these points would be\n        // shadowed, but we're ignoring that.\n        float sDi = length(lp-ro)/FAR; \n\t    float sAtt = 1./(1. + sDi*.25);\n\t    \n        // Noise layer.\n        vec3 ro2 = (ro + rd*t0)*2.5;\n        float c = noise3D(ro2)*.65 + noise3D(ro2*3.)*.25 + noise3D(ro2*9.)*.1; //cellTile\n        //float c = trigNoise3D(ro2);\n        //float c = triNoise3d(ro2/8., 0.);\n        float n = c;//max(.65-abs(c - .5)*2., 0.);//smoothstep(0., 1., abs(c - .5)*2.);\n        mist += n*sAtt;\n        \n        // Advance the starting point towards the hit point. You can \n        // do this with constant jumps (FAR/8., etc), but I'm using\n        // a variable jump here, because it gave me the aesthetic \n        // results I was after.\n        t0 += clamp(c*.25, .1, 1.);\n    }\n    \n    // Add a little noise, then clamp, and we're done.\n    return max(mist/48., 0.);\n    \n    // A different variation (float n = (c. + 0.);)\n    //return smoothstep(.05, 1., mist/32.);\n\n}\n\n//-------------------------------------------------\nvoid mainImage( out vec4 fragColor, in vec2 fragCoord )\n{\n\t// Screen coordinates.\n\tvec2 u =  (fragCoord - iResolution.xy*.5) / iResolution.y;\n\tvec2 mp = (iMouse.xy - iResolution.xy*.5) / iResolution.y;\n\t// Camera Setup.     \n\tvec3 ro = vec3(0, 1.2, iTime*SPEED);   // Camera position, doubling as the ray origin.\n    vec3 lookAt = ro + vec3(0, -.15, .5);  // \"Look At\" position.\n\t\n\t// Using the Z-value to perturb the XY-plane.\n\t// Sending the camera and \"look at\" vectors down the tunnel. The \"path\" function is \n\t// synchronized with the distance function.\n\tro.xy += path(ro.z);\n\tlookAt.xy += path(lookAt.z);\n    lookAt.x += mp.x;\n    \n    // Raising the camera up and down with the terrain function and tilting it up or down\n    // according to the slope. It's subtle, but it adds to the immersiveness of that mind \n    // blowing, endless-sand experience. :D\n    float sfH = camSurfFunc(ro); \n    float sfH2 = camSurfFunc(lookAt); \n    float slope = (sfH2 - sfH)/length(lookAt - ro); // Used a few lines below.\n    //slope = smoothstep(-.15, 1.15, (slope*.5 + .5)) - .5; // Smoothing the slope... Needs work.\n     \n    // Raising the camera with the terrain.\n    ro.y += sfH2; \n    lookAt.y += sfH2 + mp.y;\n    \n    // Using the above to produce the unit ray-direction vector.\n    float FOV = 3.14159265/2.5; // FOV - Field of view.\n    vec3 forward = normalize(lookAt - ro);\n    vec3 right = normalize(vec3(forward.z, 0, -forward.x )); \n    vec3 up = cross(forward, right);\n\n    // rd - Ray direction.\n    vec3 rd = normalize(forward + FOV*u.x*right + FOV*u.y*up);\n    \n    // Swiveling the camera about the XY-plane (from left to right) when turning corners.\n    // Naturally, it's synchronized with the path in some kind of way.\n\trd.xy = rot2( path(lookAt.z).x/96.)*rd.xy;\n    \n    // Subtle up and down tilt, or camera pitch, if you prefer.\n    rd.yz = rot2(-slope/3.)*rd.yz;\n\t\n    // Usually, you'd just make this a unit directional light, and be done with it, but I\n    // like some of the angular subtleties of point lights, so this is a point light a\n    // long distance away. Fake, and probably not advisable, but no one will notice.\n    vec3 lp = vec3(FAR*.25, FAR*.25, FAR) + vec3(0, 0, ro.z);\n \n\t// Raymarching.\n    float t = trace(ro, rd);\n    \n    gT = t;\n    \n    // Sky. Only retrieving a single color this time.\n    //vec3 sky = getSky(rd);\n    \n    vec3 col = vec3(0.9, 0.5, 0.0);   // sand color\n    \n    // Surface point. \"t\" is clamped to the maximum distance, and I'm reusing it to render\n    // the mist, so that's why it's declared in an untidy postion outside the block below...\n    // It seemed like a good idea at the time. :)\n    vec3 sp = ro+t*rd; \n    \n    float pathHeight = sp.y;//surfFunc(sp);// - path(sp.z).y; // Path height line, of sorts.\n    \n    // If we've hit the ground, color it up.\n    if (t < FAR)\n    {\n        vec3 sn = normal(sp, 1.); // Surface normal. //*(1. + t*.125)\n        \n        // Light direction vector. From the sun to the surface point. We're not performing\n        // light distance attenuation, since it'll probably have minimal effect.\n        vec3 ld = lp - sp;\n        float lDist = max(length(ld), 0.001);\n        ld /= lDist; // Normalize the light direct vector.\n        \n        lDist /= FAR; // Scaling down the distance to something workable for calculations.\n        float atten = 1./(1. + lDist*lDist*.025);\n        \n        // Texture scale factor.        \n        const float tSize = 1./8.;\n        \n        // Function based bump mapping.\n        sn = doBumpMap(sp, sn, .07);///(1. + t*t/FAR/FAR*.25)\n        \n        // Texture bump mapping.\n        float bf = .01;//(pathHeight + 5. < 0.)?  .05: .025;\n        //sn = doBumpMap(iChannel0, sp*tSize, sn, bf/(1. + t/FAR));\n        \n        // Soft shadows and occlusion.\n        float sh = softShadow(sp + sn*.002, lp, 6., t); \n        float ao = calcAO(sp, sn); // Ambient occlusion.\n        \n        // Add AO to the shadow. No science, but adding AO to things sometimes gives a bounced light look.\n        sh = min(sh + ao*.25, 1.); \n        \n        float dif = max( dot( ld, sn ), 0.0); // Diffuse term.\n        float spe = pow(max( dot( reflect(-ld, sn), -rd ), 0.0 ), 5.); // Specular term.\n        float fre = clamp(1.0 + dot(rd, sn), 0.0, 1.0); // Fresnel reflection term.\n \n        // Schlick approximation. I use it to tone down the specular term. It's pretty subtle,\n        // so could almost be aproximated by a constant, but I prefer it. Here, it's being\n        // used to give a sandstone consistency... It \"kind of\" works.\n\t\tfloat Schlick = pow( 1. - max(dot(rd, normalize(rd + ld)), 0.), 5.0);\n\t\tfloat fre2 = mix(.2, 1., Schlick);  //F0 = .2 - Hard clay... or close enough.\n       \n        // Overal global ambience. It's made up, but I figured a little occlusion (less ambient light\n        // in the corners, etc) and reflectance would be in amongst it... Sounds good, anyway. :)\n        float amb = ao*.35;// + fre*fre2*.2;\n        \n        // Coloring the soil - based on depth. Based on a line from Dave Hoskins's \"Skin Peeler.\"\n        //col = clamp(mix(vec3(1.152, 0.4275,.153), vec3(.225, 0.05985, 0.0153), -sn.y*.5 + pathHeight*.5 + 1.75), vec3(.9, 0.534375, 0.239), vec3(.9, .855, .765));\n        \n        // Give the soil a bit of a sandstone texture.\n        //col = smoothstep(-.5, 1., tex3D(iChannel0, sp*tSize, sn)*2.)*(col + vec3(.225, .21375, .19125));\n        //col = tex3D(iChannel0, sp*tSize, sn)*2.;\n        col = mix(col, vec3(0.8, 0.6, 0.2), fBm(sp.xz*16.));\n        col = mix(col*1.4, col*.6, fBm(sp.xz*32. - .5));///(1. + t*t*.001)\n        \n        //vec3 tx = tex3D(iChannel0, sp*tSize, sn); tx *= tx;\n        //col = mix(col, tx*2., .3);\n        \n        float bSurf = bumpSurf3D(sp);\n        col *= bSurf*.75 + .5;\n        //col *= surfFunc(sp)*.75 + .5;\n        \n        col = mix(col*.7 + hash(floor(sp*96.))*.3, col, min(t*t/FAR, 1.));\n        \n        col *= vec3(1.2, 1, .9);\n        \n        // Making the ground reflect just a little more to give the appearance of fine dust or sand...\n        // It's a work in progress. :)\n        //col += smoothstep(0., 1., -pathHeight - 5.5)*fre*.25;\n \n        // A bit of sky reflection. Not really accurate, but I've been using fake physics since the 90s. :)\n        col += getSky(ro, rd, ld)*fre*fre2*.5; \n        \n        // Combining all the terms from above. Some diffuse, some specular - both of which are\n        // shadowed and occluded - plus some global ambience. Not entirely correct, but it's\n        // good enough for the purposes of this demonstation.        \n        col = col*(dif + amb + vec3(1, .97, .92)*fre2*spe*2.)*atten;\n        \n        col *= sh*ao;// + amb*pow(col, vec3(2.));\n\n        //col = vec3(ao);\n    }\n    \n    // Combine the scene with the sky using some cheap volumetric substance.\n\tfloat dust = getMist(ro, rd, lp, t)*(1. - smoothstep(0., 1., pathHeight*.05));//(-rd.y + 1.);\n    vec3 gLD = normalize(lp - vec3(0, 0, ro.z));\n    vec3 sky = getSky(ro, rd, gLD);//*mix(1., .75, dust);\n    //col = mix(col, sky, min(t*t*1.5/FAR/FAR, 1.)); // Quadratic fade off. More subtle.\n    col = mix(col, sky, smoothstep(0., .95, t/FAR)); // Linear fade. Much dustier. I kind of like it.\n    \n    // Mild dusty haze... Not really sure how it fits into the physical situation, but I thought it'd\n    // add an extra level of depth... or something. At this point I'm reminded of the \"dog in a tie \n    // sitting at the computer\" meme with the caption, \"I have no idea what I'm doing.\" :D\n    vec3 mistCol = vec3(1, .95, .9); // Probably, more realistic, but less interesting.\n    //col += (mix(col, mistCol, .66)*.66 + col*mistCol*1.)*dust;\n    \n    // Simulating sun scatter over the sky and terrain: IQ uses it in his Elevated example.\n    col += vec3(1., .6, .2)*pow( max(dot(rd, gLD), 0.), 16.)*.5;\n    \n    // Applying the mild dusty haze.\n    col = col*.75 + (col + .25*vec3(1.2, 1, .9))*mistCol*dust*1.5;\n    //col *= 1.05;\n    \n    // Really artificial. Kind of cool, but probably a little too much.    \n    //col *= vec3(1.2, 1, .9);\n    \n    // Standard way to do a square vignette. Note that the maxium value value occurs at \"pow(0.5, 4.) = 1./16,\" \n    // so you multiply by 16 to give it a zero to one range. This one has been toned down with a power\n    // term to give it more subtlety.\n    u = fragCoord / iResolution.xy;\n    col = min(col, 1.)*pow( 16.*u.x*u.y*(1. - u.x)*(1. - u.y) , .0625);\n \n    // Done.\n\tfragColor = vec4(sqrt(clamp(col, 0., 1.)), 1);\n}\n",
                "description": "",
                "inputs": [
                    {
                        "channel": 0,
                        "ctype": "musicstream",
                        "id": 14736,
                        "published": 0,
                        "sampler": {
                            "filter": "linear",
                            "internal": "byte",
                            "srgb": "false",
                            "vflip": "true",
                            "wrap": "clamp"
                        },
                        "src": "https://soundcloud.com/vynehall/movements-chapter-iii"
                    }
                ],
                "name": "Image",
                "outputs": [
                    {
                        "channel": 0,
                        "id": 37
                    }
                ],
                "type": "image"
            }
        ],
        "ver": "0.1"
    }
}