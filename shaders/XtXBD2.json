{
    "Shader": {
        "info": {
            "date": "1513428180",
            "description": "Cylindrically mapping a grid of overlapping hexagonal pylons to a warped, rounded square tube... or in less verbose terms, a hexagon tiled tunnel. :)",
            "flags": 0,
            "hasliked": 0,
            "id": "XtXBD2",
            "likes": 51,
            "name": "Cylindrically Mapped Hexagons",
            "published": 3,
            "tags": [
                "raymarching",
                "tunnel",
                "hexagon",
                "polar",
                "cylinder"
            ],
            "usePreview": 0,
            "username": "Shane",
            "viewed": 1783
        },
        "renderpass": [
            {
                "code": "/*\n    Cylindrically Mapped Hexagons\n    -----------------------------\n\n\tWrapping a honeycomb structure around a winding tunnel is something I've been meaning\n\tto do for a while. I see a lot of rectangular-block tunnel imagery, so wondered how \n\thexagons would fare... The aesthetic is interesting, but I think I'd rather have the \n\ttwo hours back that it took me to code. :D Anyway, hopefully, it'll be useful to \n\tsomeone out there.\n    \n    I have no idea what purpose the weird turret-looking objects serve, but I wanted to \n\tshow that the pylons were independent of one another. Plus, the original looked a\n\tlittle static, so I added some animation - It seemed to work for Dila's \"SpikeRoom\" \n\texample, but I think the jury's out here. :)\n\n\tThis runs OK, but not fantastic, that's for sure. Fast computers will run it fine, but \n\tslower ones will probably lag a bit. As a rule, I won't put something up unless my \n\tfast computer can run it in fullscreen, and this one is borderline. When I get more \n\ttime, I'll find some ways to rectify that. The problem is the need to render neighbors \n\tto avoid artefacts. \n\n\tRendering two objects is algorithmically necessary. However, if you want to pack the \n\thexagonal pylons up tightly with no artefacts, then you realy have no other choice \n\tthan to render in groups of four. With a simplex setup, you can get\taway with three \n\tall up, but with the added overhead, it's a case of diminishing returns. At some stage,\n\tI'll look for symmetry opportunities, but none leap out at me, at present.\n\n\tI wanted to emphasize the hexagonal geometry, so went for a light and clean look...\n\tAlthough, clean is never entirely possible without antialiasing. I'll make a more \n\tstylized version at some stage. I might also put up a more simple example that's \n\teasier to decipher and less taxing on the GPU.\n\n\n\t// References:\n\n\t// A planar version.\n    Hexagonal Blocks - Shane\n\thttps://www.shadertoy.com/view/XdjyWD\n\n\t// Very basic hexagonal grid setup.\n\tMinimal Hexagonal Grid - Shane\n\thttps://www.shadertoy.com/view/Xljczw\n\n\t// Other 3D hexagon examples:\n\n\t// Hexagonal block traversal.\n\tCanal Ruins - dr2\n\thttps://www.shadertoy.com/view/4dBfzD\n\t// Based on:\n\thexwaves - mattz\n\thttps://www.shadertoy.com/view/XsBczc\n\n\t// Different kind of thing, but still awesome.\n\tGeodesic tiling - tdhooper\n\thttps://www.shadertoy.com/view/llVXRd\n\n*/\n\n#define FAR 50.\n#define TAU 6.2831853\n\n// There are two main ways you can spread objects around the radius of a circular object. One is \n// to retrieve the center position, then render a rigid object independent of the circular object's \n// curvature, which is kind of a way to say that the blocks will have flat tops, sides, etc.\n// The other, which is more fiddly, is to allow for curvature. In this case - but not always, it\n// looks a little better, so that's the default. Uncomment below, and get a visual representation.\n//\n//#define RIGID_BLOCKS\n\n\n// Standard vec2 to float hash.\nfloat hash21(vec2 p){\n    \n    return fract(sin(dot(p, vec2(7.163, 157.247)))*43758.5453); \n    \n    // n = floor(n*4.999)/4.; // For quantized heights.\n    // n = mod(p.x + p.y, 4.)/4.; // For patterns.\n}\n\n\n// Simple hash.\nfloat hash(float n){ return fract(sin(n)*43758.5453); }  \n\n\n// 2x2 matrix rotation.\nmat2 rot2(float a){ float c = cos(a), s = sin(a); return mat2(c, -s, s, c); }\n\n\n// This is a rewrite of IQ's original. It's self contained, which makes it much\n// easier to copy and paste. I've also tried my best to minimize the amount of \n// operations to lessen the work the GPU has to do, but I think there's room for\n// improvement.\n//\nfloat n3D(vec3 p){\n    \n    // Just some random figures, analogous to stride. You can change this, if you want.\n\tconst vec3 s = vec3(7, 157, 113);\n\t\n\tvec3 ip = floor(p); // Unique unit cell ID.\n    \n    // Setting up the stride vector for randomization and interpolation, kind of. \n    // All kinds of shortcuts are taken here. Refer to IQ's original formula.\n    vec4 h = vec4(0., s.yz, s.y + s.z) + dot(ip, s);\n    \n\tp -= ip; // Cell's fractional component.\n\t\n    // A bit of cubic smoothing, to give the noise that rounded look.\n    p = p*p*(3. - 2.*p);\n    \n    // Standard 3D noise stuff. Retrieving 8 random scalar values for each cube corner,\n    // then interpolating along X. There are countless ways to randomize, but this is\n    // the way most are familar with: fract(sin(x)*largeNumber).\n    h = mix(fract(sin(h)*43758.5453), fract(sin(h + s.x)*43758.5453), p.x);\n\t\n    // Interpolating along Y.\n    h.xy = mix(h.xz, h.yw, p.y);\n    \n    // Interpolating along Z, and returning the 3D noise value.\n    return mix(h.x, h.y, p.z); // Range: [0, 1].\n\t\n}\n\n// Approximating - very roughly - the metallic Shadertoy texture. I handcoded this to \n// keep algorithmic art purists - like Dr2 - happy. :)\nvec3 tex(in vec3 p){\n    \n    float ns = n3D(p)*.57 + n3D(p*2.)*.28 + n3D(p*4.)*.15;\n\n    // Some fBm noise based bluish red coloring.\n    vec3 n = mix(vec3(.33, .11, .022), vec3(.385, .55, .715), ns);\n    n *= mix(vec3(1, .9, .8), vec3(0, .1, .2), n3D(p*32.))*.6 + .4;\n    \n    //n =  n*.3 + min(n.zyx*vec3(1.3, .6, .2)*.75, 1.)*.7;\n    \n    return clamp(n, 0., 1.);\n\n}\n\n\n// Tri-Planar blending function. Based on an old Nvidia tutorial.\nvec3 tex3D( sampler2D t, in vec3 p, in vec3 n ){\n    \n    n = max(abs(n) - .2, 0.001); // n = max(abs(n), 0.001), etc.\n    n /= dot(n, vec3(1));\n\tvec3 tx = texture(t, p.yz).xyz;\n    vec3 ty = texture(t, p.zx).xyz;\n    vec3 tz = texture(t, p.xy).xyz;\n    \n    // Textures are stored in sRGB (I think), so you have to convert them to linear space \n    // (squaring is a rough approximation) prior to working with them... or something like that. :)\n    // Once the final color value is gamma corrected, you should see correct looking colors.\n    return (tx*tx*n.x + ty*ty*n.y + tz*tz*n.z);\n}\n\n\n\n\n\n// The path is a 2D sinusoid that varies over time, depending upon the frequencies, and amplitudes.\nvec2 path(in float z){ \n\n    //return vec2(0); // Straight path.\n    return vec2(sin(z*.085)*7., cos(z*.085)*4.); // Windy path.\n    \n}\n\n\n// One of many ways to blink something on and off smoothly. Here it's used to pop some of\n// the pylons up and down, and to change the color shades.\nfloat blinkFunc(float n){\n    \n    //return step(.666, n); // No animation.\n    return smoothstep(.85, .95, sin(n*TAU + iTime*1.5)); // Animation.\n}\n\n\n\n// Hacky globals to hold hexagon pylon values, the spike values, and the pylon's random ID. Putting\n// these in a struct, or \"mat4,\"  might be neater, but I'm never quite sure how the compiler will deal with it.\n// Chances are, it won't make a difference, so I probably should.\nvec4 sh;\nvec4 spike4;\nvec4 rnd;\n\n\n// The pylon object. Hacked away on the spot, but basically a hexagonal box, with a hexagonal\n// spike and some hacky sinusoidal decoration.\nfloat objectDetail(vec3 p, vec3 b, float rnd, inout float spike){\n    \n    \n    // X: Height. YZ: Cross section - Not intuitive, but that's how things fell out.\n    \n    // Standard hexagonal conversion.\n    p = abs(p);\n    p.yz = vec2(p.y*.8660254 + p.z*.5, p.z);\n    \n    // Using the hexagonal field for a bit of decoration.\n    float lines = clamp(-cos(max(p.y, p.z)*TAU*3.)*4. + 3.95, 0., 1.);\n    \n    // Gets rid of the central dot on the face, but I prefer it as is.\n    //if(length(p.yz)<.1) lines = 1.;\n    \n    // Moving the colored pylons up and down every once so often. The blink function\n    // is coordinated with the color in the rendering section.\n    float blink = blinkFunc(rnd);\n    b.x += (blink)/6.;\n    \n    // Pylon dimensions.\n    p -= b;\n    \n    // Main hexagonal pylon.\n    float h1 = min(max(max(p.x, p.y), p.z), 0.) + length(max(p, 0.)) - .01;\n    \n    // Spike object - Just a resized hexagonal pylon.\n    p += vec3(-.25, .225, .225);\n    float h2 = min(max(max(p.x, p.y), p.z), 0.) + length(max(p, 0.)) - .0;\n    \n    // Spike identification.    \n    spike = h1<h2? 0. : 1.;\n    \n    // Mix the spike into the main pylon according to the \"blink\" function. When\n    // the blink function is zero, there is no spike. When it's \"1,\" the spike is\n    // fully extended (and the block is colored), and there's a smooth \n    // transition in between.\n    h1 = mix(h1, min(h1, h2), blink);\n    \n    // Adding the edge decoration.\n    h1 += lines*.01;\n    \n    \n    // Return the object.\n    return h1;    \n    \n}\n\n// Wrapping the non-rigid coordinate. Note the hacky figure on the end... It seems to be dependent\n// on the setting for the tunnel radius. There's some tunnel wall mutation involved, so it's\n// fudged a little.\nfloat convert(float a, float aNum){\n    \n    return (mod(a/TAU, 1./aNum) - .5/aNum)*TAU*1.6;\n    \n}\n\n// Cylindrically mapping a grid of overlapping hexagonal pylons to a warped, rounded square tube - \n// Otherwise known as a hexagon tiled tunnel. :)\n//\n// I've done my best to explain the process, but if you're anything like me, you don't enjoy \n// trying to decipher other people's custom esoteric functions. Plenty of people on here could\n// probably code this in their sleep, but if you're not sure how go about it, start by rendering a \n// square grid of overlapping square columns to a flat - or undulating - plane, then progress to \n// hexagons.\n\n// Afterward, map the square pylons to a cylinder, and finally, the hexagonal ones. If any of it \n// troubles you, use this as a guide... and if you find ways to improve it along the way, feel \n// free to let me know. :)\n//\nfloat map(vec3 p){\n    \n    // Wrap the tunnel (polar mapped hexagonal pylons) around the path.\n    p.xy -= path(p.z);\n    \n    // Scaling along Z. Changing this value requires tile diameter, tile number and\n    // tunnel width adjustments.\n    const float zScale = 1.5;\n    \n    // Number of pylon segments spread around the tunnel. Actually, the final count is\n    // twice this amount, due to rendering two pylons side by side. The other two are\n    // rendered in front in the X direction.\n    const float aNum = 8.;\n    \n\n    // Radius of the tunnel. The radial polar coordinate with be edged out by that amount.\n    const float tunRad = 1.63;\n    \n    // Effectively the length of the blocks. Set to something deep enough to block out all\n    // the light. Set it to something like \".1\" to see thin tiles.\n    const float blockRad = 1.;\n    \n    \n    // Just an extra factor that controls random height.\n    float rndFactor = .5;\n    \n    \n    // Hexagonal block dimensions. Play around with them to see what they do.\n    const vec3 wd = vec3(blockRad, .235*zScale, .235*zScale);\n    \n    // Holding vector for the cylindrical coordinates. \n    vec3 pC;\n    \n    // A cheap way to polar map a squarish tunnel. Simply mutate the the regular tunnel\n    // wall positions a bit. If you warp things too much, the blocks will get too \n    // distorted, but this here isn't too noticeable.\n    //vec2 mut = pow(abs(p.xy), vec2(4))*.125 + .875; // Etc.\n    vec2 mut = p.xy*p.xy*.125 + .875;\n    p.xy *= mut*vec2(1, 1.2);\n    \n    //p += sin(p*2. + cos(p.yzx*2.))*.1; // Adding bumps. Too much for this example.\n    \n    // Scaling Z... This gave me more trouble than I care to admit. Scale here, then\n    // scale back when doing the repetition. Simple... now. :)\n    p.z /= zScale;\n    \n    // Relative Z distances of the four tiles. The bit on the end belongs there, but\n    // I can't remember why. Some kind of correction in order to get the get correct height\n    // values for wall tiling... or something. I should write these things down. :D\n    vec4 fPz = floor(vec4(0, .5, .25, .75) + p.z) - vec4(0, .5, .25, .75);\n    \n\n    // Angle and angular index.\n    float a, ia;\n    \n\n    //// Hexagon 1.    \n    // Standard polar mapping stuff.\n    a = atan(p.y, p.x);\n    ia = floor(a/TAU*aNum);\n    rnd.x = hash21(vec2(ia, fPz.x));\n    #ifdef RIGID_BLOCKS\n    p.xy = rot2((ia + .5)*TAU/aNum)*p.xy;\n    pC = vec3(p.xy, p.z);\n    //pp = vec3(length(p.xy)*vec2(cos((ia + .5)*TAU/aNum), sin((ia + .5)*TAU/aNum)), p.z);\n    #else\n    p.xy = rot2(a)*p.xy;\n    pC = vec3(p.xy, p.z); pC.y = convert(a, aNum);\n    #endif\n    // First entry when you perform: pp.x = mod(pp.x, rad) - rad/2.;\n    pC.x -= tunRad - rnd.x*rndFactor + blockRad; // Base tunnel width.\n    pC.z = (mod(pC.z, 1.) - .5)*zScale; // Repetition along the tunnel.\n    \n    sh.x = objectDetail(pC, wd, rnd.x, spike4.x); // The hexagon pylon object and spike.\n    \n\n    //// Hexagon 2.   \n    rnd.y = hash21(vec2(ia + .5, fPz.y));\n    #ifdef RIGID_BLOCKS    \n    pC = vec3(p.xy, p.z + .5);\n    #else\n    pC = vec3(p.xy, p.z  + .5); pC.y = convert(a, aNum);\n    #endif\n    pC.x -= tunRad - rnd.y*rndFactor + blockRad; // Tunnel width.\n    pC.z = (mod(pC.z, 1.) - .5)*zScale;\n    \n    sh.y = objectDetail(pC, wd, rnd.y, spike4.y);\n    \n    //p.xy = q.xy;\n\t//// Hexagon 3.  \n    #ifdef RIGID_BLOCKS\n    //p.xy = q.xy;\n    p.xy = rot2(-(ia + .5)*TAU/aNum + 3.14159/aNum)*p.xy;\n    #else\n    p.xy = rot2(-a + 3.14159/aNum)*p.xy;\n    #endif\n    a = atan(p.y, p.x);\n    ia = floor(a/TAU*aNum);\n    \n    rnd.z = hash21(vec2(ia, fPz.z));\n    #ifdef RIGID_BLOCKS\n    p.xy = rot2((ia + .5)*TAU/aNum)*p.xy;\n    pC = vec3(p.xy, p.z + .25);\n    #else\n    p.xy = rot2(a)*p.xy;\n    pC = vec3(p.xy, p.z + .25); pC.y = convert(a, aNum);\n    #endif\n    pC.x -= tunRad - rnd.z*rndFactor + blockRad; // Tunnel width.\n    pC.z = (mod(pC.z, 1.) - .5)*zScale;\n    sh.z = objectDetail(pC, wd, rnd.z, spike4.z);\n   \n  \n\t//// Hexagon 4. \n    rnd.w = hash21(vec2(ia + .5, fPz.w));\n    #ifdef RIGID_BLOCKS\n    pC = vec3(p.xy, p.z + .75);\n    #else\n    pC = vec3(p.xy, p.z + .75); pC.y = convert(a, aNum);\n    #endif\n    pC.x -= tunRad - rnd.w*rndFactor + blockRad; // Tunnel width.\n    pC.z = (mod(pC.z, 1.) - .5)*zScale;\n    sh.w = objectDetail(pC, wd, rnd.w, spike4.w);\n    \n    // Determining the minimum hexagon pylon distance, then returning it.\n    vec2 hx2 = min(sh.xy, sh.zw);\n    return min(hx2.x, hx2.y)*.85;\n \n}\n\n\n// Normal calculation, with some edging and curvature bundled in.\nvec3 getNormal(vec3 p, inout float edge, inout float crv, float ef) { \n\t\n    // Roughly two pixel edge spread, but increased slightly with larger resolution.\n    vec2 e = vec2(ef/mix(450., iResolution.y, .5), 0);\n\n\tfloat d1 = map(p + e.xyy), d2 = map(p - e.xyy);\n\tfloat d3 = map(p + e.yxy), d4 = map(p - e.yxy);\n\tfloat d5 = map(p + e.yyx), d6 = map(p - e.yyx);\n\tfloat d = map(p)*2.;\n\n    edge = abs(d1 + d2 - d) + abs(d3 + d4 - d) + abs(d5 + d6 - d);\n    //edge = abs(d1 + d2 + d3 + d4 + d5 + d6 - d*3.);\n    edge = smoothstep(0., 1., sqrt(edge/e.x*2.));\n\n    /*\n    // Wider sample spread for the curvature.\n    e = vec2(12./450., 0);\n\td1 = map(p + e.xyy), d2 = map(p - e.xyy);\n\td3 = map(p + e.yxy), d4 = map(p - e.yxy);\n\td5 = map(p + e.yyx), d6 = map(p - e.yyx);\n    crv = clamp((d1 + d2 + d3 + d4 + d5 + d6 - d*3.)*32. + .5, 0., 1.);\n\t*/\n    \n    e = vec2(.0015, 0); //iResolution.y - Depending how you want different resolutions to look.\n\td1 = map(p + e.xyy), d2 = map(p - e.xyy);\n\td3 = map(p + e.yxy), d4 = map(p - e.yxy);\n\td5 = map(p + e.yyx), d6 = map(p - e.yyx);\n\t\n    return normalize(vec3(d1 - d2, d3 - d4, d5 - d6));\n}\n\n\n// I keep a collection of occlusion routines... OK, that sounded really nerdy. :)\n// Anyway, I like this one. I'm assuming it's based on IQ's original.\nfloat calculateAO(in vec3 p, in vec3 n)\n{\n\tfloat sca = 2., occ = 0.0;\n    for( int i=0; i<6; i++ ){\n    \n        float hr = .01 + float(i)*.5/5.;        \n        float dd = map(p + hr*n);\n        occ += (hr - dd)*sca;\n        sca *= .7;\n    }\n    return clamp(1. - occ, 0., 1.);   \n    \n}\n\n\n// Using a variation of IQ's AO function to calculate thickness, but with the normal flipped.\n// IQ uses a similar setup to calculate SSS... Subsurface AO, I guess you'd call it.\nfloat thickness(in vec3 p, in vec3 n){\n    \n    const float sNum = 4.;\n    float sca = 1., occ = 0.;\n    for(float i=0.; i<sNum + .001; i++ ){\n    \n        float hr = 0.05 + .4*i/sNum; \n        //vec3 rn = normalize(n + RandomHemisphereDir(n, hr)*rad*.5);\n        float dd = map(p - n*hr);\n        occ += (hr - min(dd, 0.))*sca;\n        sca *= .9;\n    }\n    return 1. - max(occ/sNum, 0.); \n    \n}\n\n\n// Simple environment mapping. Pass the reflected vector in and create some colored noise with it.\nvec3 envMap(vec3 rd){\n    \n  \n    float c = n3D(rd*4.)*.66 + n3D(rd*8.)*.34; // Noise value.\n    c = smoothstep(.3, 1., c); // Darken and add contast for more of a spotlight look.\n    \n    return vec3(c*c*c, c*c, c); // Simple, cool coloring.\n    \n}\n\nvoid mainImage( out vec4 fragColor, in vec2 fragCoord ){\n\t\n\t// Screen coordinates.\n\tvec2 uv = (fragCoord - iResolution.xy*0.5)/iResolution.y;\n\t\n\t// Camera Setup.\n\tvec3 camPos = vec3(0, 0, iTime*4.); // Camera position, doubling as the ray origin.\n\tvec3 lookAt = camPos + vec3(0, 0, .5);  // \"Look At\" position.\n \n    // Light positioning. Both are behind the camera, and placed in a rough headlight\n    // position. One light probably would have done the same thing.\n \tvec3 light_pos = camPos + vec3(-.35, 0, -.25);// Left light.\n\tvec3 light_pos2 = camPos + vec3(.35, 0, -.25);// Right light.\n\n\t// Using the Z-value to perturb the XY-plane.\n\t// Sending the camera, \"look at,\" and two light vectors down the tunnel. The \"path\" function is \n\t// synchronized with the distance function.\n\tlookAt.xy += path(lookAt.z);\n\tcamPos.xy += path(camPos.z);\n\tlight_pos.xy += path(light_pos.z);\n\tlight_pos2.xy += path(light_pos2.z);\n\n    // Using the above to produce the unit ray-direction vector.\n    float FOV = 3.14159265/3.; // FOV - Field of view.\n    vec3 forward = normalize(lookAt-camPos);\n    vec3 right = normalize(vec3(forward.z, 0., -forward.x )); \n    vec3 up = cross(forward, right);\n    \n    // rd - Ray direction.\n    //vec3 rd = normalize(forward + FOV*uv.x*right + FOV*uv.y*up);\n    \n    // Slight bulbous scene warp.\n    vec3 rd = (forward + FOV*uv.x*right + FOV*uv.y*up);\n    rd = normalize(vec3(rd.xy, rd.z - length(rd.xy)*.15));\n\n    \n    // Swiveling the camera from left to right when turning corners.\n    rd.xy = rot2( path(lookAt.z).x/32. )*rd.xy;\n/*\n    \n    // Mouse controls, as per Dave Hoskins's suggestion. A bit hacky, but I'll fix them.    \n\tvec2 ms = vec2(0);\n    //if (iMouse.z > 1.0) \n    ms = (2.*iMouse.xy - iResolution.xy)/iResolution.xy;\n    vec2 a = sin(vec2(1.5707963, 0) - ms.x); \n    mat2 rM = mat2(a, -a.y, a.x);\n    rd.xz = rd.xz*rM; \n    a = sin(vec2(1.5707963, 0) - ms.y); \n    rM = mat2(a, -a.y, a.x);\n    rd.yz = rd.yz*rM;\n*/\n\t\t\n    // Standard ray marching routine. I find that some system setups don't like anything other than\n    // a \"break\" statement (by itself) to exit. \n\tfloat t = 0., dt;\n\tfor(int i=0; i<96; i++){\n\t\tdt = map(camPos + rd*t);\n\t\tif(abs(dt)<.001*(t*.125 + 1.) || t>FAR){ break; } \n\t\tt += dt;\n\t}\n    \n    t = min(t, FAR);\n    \n    \n    // Obtaining the random ID and spike ID, in a very lazy fashion.\n    float rndID = (sh.x<sh.y && sh.x<sh.z && sh.x<sh.w)? rnd.x : (sh.y<sh.z && sh.y<sh.w)? rnd.y : (sh.z<sh.w)? rnd.z : rnd.w; \n    float svSpike = (sh.x<sh.y && sh.x<sh.z && sh.x<sh.w)? spike4.x : (sh.y<sh.z && sh.y<sh.w)? spike4.y : (sh.z<sh.w)? spike4.z : spike4.w;\n    \n\n \t\n    // The final scene color. Initated to black.\n\tvec3 sceneCol = vec3(0.);\n\t\n\t// The ray has effectively hit the surface, so light it up.\n\tif(t<FAR){\n\t\n\n    \t\n    \t// Surface position and surface normal.\n\t    vec3 sp = t * rd+camPos;\n        float edge = 0., crv = 1., ef = 6.;\n\t    vec3 sn = getNormal(sp, edge, crv, ef);//getNormal(sp);\n        \n        // Texture scale factor.\n        const float tSize0 = 1./2.; \n        const float tSize1 = 1./2.;\n    \t\n\t    \n\t    // Ambient occlusion.\n\t    float ao = calculateAO(sp, sn);\n    \t\n    \t// Light direction vectors.\n\t    vec3 ld = light_pos-sp;\n\t    vec3 ld2 = light_pos2-sp;\n\n        // Distance from respective lights to the surface point.\n\t    float distlpsp = max(length(ld), 0.001);\n\t    float distlpsp2 = max(length(ld2), 0.001);\n    \t\n    \t// Normalize the light direction vectors.\n\t    ld /= distlpsp;\n\t    ld2 /= distlpsp2;\n\t    \n\t    // Light attenuation, based on the distances above. In case it isn't obvious, this\n        // is a cheap fudge to save a few extra lines. Normally, the individual light\n        // attenuations would be handled separately... No one will notice, nor care. :)\n\t    float atten1 = 1./(1. + distlpsp*.125 + distlpsp*distlpsp*.025);\n\t    float atten2 = 1./(1. + distlpsp2*.125 + distlpsp2*distlpsp2*.025);\n        \n    \t// Ambient light.\n\t    float ambience = 0.25;\n    \t\n    \t// Diffuse lighting.\n\t    float diff = max( dot(sn, ld), 0.0);\n\t    float diff2 = max( dot(sn, ld2), 0.0);\n        //diff = diff*diff*2.;\n        //diff2 = diff2*diff2*2.;\n    \t\n    \t// Specular lighting.\n\t    float spec = pow(max( dot( reflect(-ld, sn), -rd ), 0.0 ), 8.);\n\t    float spec2 = pow(max( dot( reflect(-ld2, sn), -rd ), 0.0 ), 8.);\n    \t\n\t    \n\t    // Fresnel term. Good for giving a surface a bit of a reflective glow.\n        //float fre = pow( clamp(dot(sn, rd) + 1., .0, 1.), 1.);\n        \n        \n        // Obtaining the texel color.\n\t\t//\n        vec3 tP = sp;\n        //tP.xy -= path(sp.z); // Wrapping the texture around the path. \n\n        vec3 texCol = tex(tP*tSize0*3.); \n        texCol = smoothstep(0., .5, texCol);\n        texCol = mix(texCol, vec3(1), .7);\n\n\t\t// Blink function. It's off (zero) mostly, then spikes to on (one) smoothly, just\n        // for a split second. Hence, a blink of sorts. Anyway, color the hexagonal pylon\n        // (main part and spike) when it's in blink mode.\n        float blink = blinkFunc(rndID);;\n\n        // If it's a moving pylon, but not the spike part, color it up and give it a tiny\n        // bit of glow.\n        if(svSpike<.5) {\n            \n            float th = thickness(sp, sn);\n            vec3 thCol = texCol*vec3(1, .3, .0)*pow(th, 4.)*8.;\n\n            float blend = dot(sin(sp*TAU/6. - cos(sp.yzx*TAU/6.)*TAU/2.), vec3(.166)) + .5;\n            vec3 blinkCol = vec3(1, .1, .2);\n            blinkCol = mix(blinkCol, blinkCol.zyx, hash(rndID + .37));\n            //thCol = mix(thCol, thCol.zyx, hash(rndID + .37));\n\n\n            if(hash(rndID)>.66) texCol = mix(texCol*.5*2., (texCol*blinkCol*3. + thCol), blink);\n            else texCol = mix(texCol*.5*2., texCol*vec3(1.3, .8, .4) + thCol, blink);\n            \n        }\n        else if(blink > 0.){ // If it's the spike part of the moving pylon, darken the shade.\n            texCol *= .7;\n        }\n            \n                \n         \n    \t// Combining the above terms to produce the final color.\n        //\n        sceneCol = (texCol*(diff*vec3(1) + ao*0.35) + spec*vec3(.5, .7, 1)*2.)*atten1;\n        sceneCol += (texCol*(diff2*vec3(1) + ao*0.35) + spec2*vec3(.5, .7, 1)*2.)*atten2;\n        \n        // Fake environment mapping: It doesn't make a great deal of physical sense in this scenario, \n        // but it gives the impression that some mild reflection is happening.\n        sceneCol += envMap(reflect(rd, sn))*(atten1 + atten2)*.5;\n            \n       \n\t    \n        // Applying the ambient occlusion and dark edges.\n        sceneCol *= ao*(1. - edge*.9);\n        \n         \n        // Very basic grey tone - for debugging purposes.\n        //sceneCol = vec3(1)*2./(1.+ t*t*.25)*(1. - edge*.9);\n\t   \n\t\n\t}\n    \n    \n    // Blending in some distance fog.\n    sceneCol = mix(sceneCol, mix(vec3(1), vec3(.5, .6, 1), rd.y*.5 + .5), smoothstep(0., .9, t/FAR));\n    \n\n/*\n\t// Cheap cross hatching. Borrowed from one of my other shaders.\n    vec2 oP = uv;\n    const float sc = 1.25;\n    // Cheap postprocess hash. Interesting, and looks cool with other examples, but \n\t// possibly a little too much grunge, in this instance. Anyway, I've left it here \n\t// for anyone who's like to take a look.\n    float gr = dot(sceneCol, vec3(.299, .587, .114))*1.;\n    sceneCol *= vec3(1.5);\n    oP = rot2(3.14159/3.)*oP;\n    if(gr<.125) sceneCol *= clamp(sin((oP.x - oP.y)*6.283*96./sc)*1. + .95, 0., 1.)*.7 + .3;\n    oP = rot2(3.14159/3.)*oP;\n    if(gr<.25) sceneCol *= clamp(sin((oP.x - oP.y)*6.283*96./sc)*1. + .95, 0., 1.)*.7 + .3;\n    oP = rot2(3.14159/3.)*oP;\n    if(gr<.5) sceneCol *= clamp(sin((oP.x - oP.y)*6.283*96./sc)*1. + .95, 0., 1.)*.7 + .3;\n*/  \n    \n    \n\t// Rough gamma correction.\n\tfragColor = vec4(sqrt(max(sceneCol, 0.)), 1);\n\t\n}",
                "description": "",
                "inputs": [],
                "name": "Image",
                "outputs": [
                    {
                        "channel": 0,
                        "id": 37
                    }
                ],
                "type": "image"
            }
        ],
        "ver": "0.1"
    }
}