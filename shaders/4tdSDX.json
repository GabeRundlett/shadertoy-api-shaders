{
    "Shader": {
        "info": {
            "date": "1483684461",
            "description": "I had a lot of trouble getting the snow to look right. It's good enough, but it could still be better.",
            "flags": 0,
            "hasliked": 0,
            "id": "4tdSDX",
            "likes": 80,
            "name": "Iceberg",
            "published": 3,
            "tags": [
                "iceberg"
            ],
            "usePreview": 1,
            "username": "otaviogood",
            "viewed": 6983
        },
        "renderpass": [
            {
                "code": "/*--------------------------------------------------------------------------------------\nLicense CC0 - http://creativecommons.org/publicdomain/zero/1.0/\nTo the extent possible under law, the author(s) have dedicated all copyright and related and neighboring rights to this software to the public domain worldwide. This software is distributed without any warranty.\n----------------------------------------------------------------------------------------\n^ This means do ANYTHING YOU WANT with this code. Because we are programmers, not lawyers.\n-Otavio Good\n*/\n\n// ---------------- Config ----------------\n// This is an option that lets you render high quality frames for screenshots. It enables\n// stochastic antialiasing and motion blur automatically for any shader.\n//#define NON_REALTIME_HQ_RENDER\nconst float frameToRenderHQ = 15.1; // Time in seconds of frame to render\nconst float antialiasingSamples = 16.0; // 16x antialiasing - too much might make the shader compiler angry.\n\n//#define MANUAL_CAMERA\n// Some computers were crashing, so I scaled this down by default.\n//#define HQ_NOISE\n\n// Makes compile times much faster.\n// Forces for loops to not unroll because compiler thinks the zero is not a constant.\n#define ZERO_TRICK max(0, -iFrame)\n\n// --------------------------------------------------------\n// These variables are for the non-realtime block renderer.\nfloat localTime = 0.0;\nfloat seed = 1.0;\n\n// Animation variables\nfloat fade = 1.0;\nfloat exposure = 1.0;\n\n// lighting vars\nvec3 sunDir = normalize(vec3(0.93, 1.0, 1.0));\nconst vec3 sunCol = vec3(250.0, 220.0, 200.0) / 3555.0;\nconst vec3 horizonCol = vec3(0.95, 0.95, 0.95)*1.3;\nconst vec3 skyCol = vec3(0.03,0.45,0.95);\nconst vec3 groundCol = vec3(0.003,0.7,0.75);\n\n// ---- noise functions ----\nfloat v31(vec3 a)\n{\n    return a.x + a.y * 37.0 + a.z * 521.0;\n}\nfloat v21(vec2 a)\n{\n    return a.x + a.y * 37.0;\n}\nfloat Hash11(float a)\n{\n    return fract(sin(a)*10403.9);\n}\nfloat Hash21(vec2 uv)\n{\n    float f = uv.x + uv.y * 37.0;\n    return fract(sin(f)*104003.9);\n}\nvec2 Hash22(vec2 uv)\n{\n    float f = uv.x + uv.y * 37.0;\n    return fract(cos(f)*vec2(10003.579, 37049.7));\n}\nvec2 Hash12(float f)\n{\n    return fract(cos(f)*vec2(10003.579, 37049.7));\n}\n// noise functions\nfloat Hash2d(vec2 uv)\n{\n    float f = uv.x + uv.y * 37.0;\n    return fract(sin(f)*104003.9);\n}\nfloat Hash3d(vec3 uv)\n{\n    float f = uv.x + uv.y * 37.0 + uv.z * 521.0;\n    return fract(sin(f)*110003.9);\n}\nfloat mixP(float f0, float f1, float a)\n{\n    return mix(f0, f1, a*a*(3.0-2.0*a));\n}\nconst vec2 zeroOne = vec2(0.0, 1.0);\nfloat noise1d(float uv)\n{\n    float fr = fract(uv);\n    float fl = floor(uv);\n    float h0 = Hash11(fl);\n    float h1 = Hash11(fl + 1.0);\n    return mixP(h0, h1, fr);\n}\nfloat noise2d(vec2 uv)\n{\n    vec2 fr = fract(uv.xy);\n    vec2 fl = floor(uv.xy);\n    float h00 = Hash2d(fl);\n    float h10 = Hash2d(fl + zeroOne.yx);\n    float h01 = Hash2d(fl + zeroOne);\n    float h11 = Hash2d(fl + zeroOne.yy);\n    return mixP(mixP(h00, h10, fr.x), mixP(h01, h11, fr.x), fr.y);\n}\nfloat noiseValue(vec3 uv)\n{\n    vec3 fr = fract(uv.xyz);\n    vec3 fl = floor(uv.xyz);\n    float h000 = Hash3d(fl);\n    float h100 = Hash3d(fl + zeroOne.yxx);\n    float h010 = Hash3d(fl + zeroOne.xyx);\n    float h110 = Hash3d(fl + zeroOne.yyx);\n    float h001 = Hash3d(fl + zeroOne.xxy);\n    float h101 = Hash3d(fl + zeroOne.yxy);\n    float h011 = Hash3d(fl + zeroOne.xyy);\n    float h111 = Hash3d(fl + zeroOne.yyy);\n    return mixP(\n        mixP(mixP(h000, h100, fr.x),\n             mixP(h010, h110, fr.x), fr.y),\n        mixP(mixP(h001, h101, fr.x),\n             mixP(h011, h111, fr.x), fr.y)\n        , fr.z);\n}\n\n\nconst float PI=3.14159265;\n\nvec3 saturate(vec3 a) { return clamp(a, 0.0, 1.0); }\nvec2 saturate(vec2 a) { return clamp(a, 0.0, 1.0); }\nfloat saturate(float a) { return clamp(a, 0.0, 1.0); }\n\nvec3 RotateX(vec3 v, float rad)\n{\n  float cos = cos(rad);\n  float sin = sin(rad);\n  return vec3(v.x, cos * v.y + sin * v.z, -sin * v.y + cos * v.z);\n}\nvec3 RotateY(vec3 v, float rad)\n{\n  float cos = cos(rad);\n  float sin = sin(rad);\n  return vec3(cos * v.x - sin * v.z, v.y, sin * v.x + cos * v.z);\n}\nvec3 RotateZ(vec3 v, float rad)\n{\n  float cos = cos(rad);\n  float sin = sin(rad);\n  return vec3(cos * v.x + sin * v.y, -sin * v.x + cos * v.y, v.z);\n}\n\n// This function basically is a procedural environment map that makes the sun\nvec3 GetSunColorSmall(vec3 rayDir, vec3 sunDir)\n{\n\tvec3 localRay = normalize(rayDir);\n\tfloat dist = 1.0 - (dot(localRay, sunDir) * 0.5 + 0.5);\n\tfloat sunIntensity = 0.05 / dist;\n    sunIntensity += exp(-dist*150.0)*7000.0;\n\tsunIntensity = min(sunIntensity, 40000.0);\n\treturn sunCol * sunIntensity*0.2;\n}\n\nvec3 GetEnvMap(vec3 rayDir, vec3 sunDir)\n{\n    // fade the sky color, multiply sunset dimming\n    vec3 finalColor = mix(horizonCol, skyCol, pow(saturate(rayDir.y), 0.47))*0.95;\n    // make clouds - just a horizontal plane with noise\n    float n = noise2d(rayDir.xz/rayDir.y*1.0);\n    n += noise2d(rayDir.xz/rayDir.y*2.0)*0.5;\n    n += noise2d(rayDir.xz/rayDir.y*4.0)*0.25;\n    n += noise2d(rayDir.xz/rayDir.y*8.0)*0.125;\n    n = pow(abs(n), 3.0);\n    n = mix(n * 0.2, n, saturate(abs(rayDir.y * 8.0)));  // fade clouds in distance\n    finalColor = mix(finalColor, (vec3(1.0)+sunCol*10.0)*0.75*saturate((rayDir.y+0.2)*5.0), saturate(n*0.125));\n\n    // add the sun\n    finalColor += GetSunColorSmall(rayDir, sunDir);\n    return finalColor;\n}\n\n// min function that supports materials in the y component\nvec2 matmin(vec2 a, vec2 b)\n{\n    if (a.x < b.x) return a;\n    else return b;\n}\n\n// ---- shapes defined by distance fields ----\n// See this site for a reference to more distance functions...\n// https://iquilezles.org/articles/distfunctions\n\n// signed box distance field\nfloat sdBox(vec3 p, vec3 radius)\n{\n  vec3 dist = abs(p) - radius;\n  return min(max(dist.x, max(dist.y, dist.z)), 0.0) + length(max(dist, 0.0));\n}\n\nfloat cyl(vec3 p, float rad)\n{\n    return length(p.xy) - rad;\n}\n\nfloat sSphere(vec3 p, float rad)\n{\n    return length(p) - rad;\n}\n\n// k should be negative. -4.0 works nicely.\n// smooth blending function\nfloat smin(float a, float b, float k)\n{\n\treturn log2(exp2(k*a)+exp2(k*b))/k;\n}\n\nconst float sway = 0.015;\n\n// This is the distance function that defines all the scene's geometry.\n// The input is a position in space.\n// The output is the distance to the nearest surface, a material index,\n// and the difference between the iceberg distance and the water distance.\nvec3 DistanceToObject(vec3 p)\n{\n    float dist = p.y;\n\n    if (abs(dist) < 0.07)  // Only calculate noise if we are close.\n    {\n        // Sum up different frequencies of noise to make water waves.\n        float waterNoise = noise2d(p.xz*4.0+localTime)*0.1 +\n            noise2d(p.xz*8.0+localTime)*0.03 +\n            noise2d(p.xz*16.0-localTime)*0.015 +\n            noise2d(p.xz*32.0-localTime)*0.005 +\n            noise2d(p.xz*64.0-localTime)*0.002;\n        // Fade the waves a bit in the distance.\n    \tdist += waterNoise * 0.2 * saturate(8.0/length(p.xz));\n    }\n    vec2 water = vec2(dist, 1.0);\n\n    p = RotateZ(p, sin(localTime)*sway);\n    // Sculpt the iceberg.\n    float slant = (p.y + p.x*0.25) / 1.0307;\n    slant -= cos(p.z*2.0)*0.0625;\n    dist = sSphere(p, 2.0) + sin(p.z*4.0)*0.125;\n    dist = smin(dist, sSphere(p + vec3(1.0, 11.85, 0.0), 12.0), -5.0);\n    float chop = cyl(p.xzy + vec3(1.5), 1.5);\n    float chop2 = cyl(p.xyz + vec3(0.0, -0.5, 0.0), 0.6) + sin(p.z*2.0)*0.125;\n    chop2 = min(chop2, -slant + 1.6);\n    chop2 = min(chop2, sdBox(p + vec3(-1.75, -0.74, -2.0), vec3(0.7)));\n    chop = smin(chop, chop2, -10.0);\n    chop = min(chop, chop2);\n    dist = -smin(-dist, chop, -30.0);\n    if (abs(dist) < 0.5)  // Only calculate noise if we are close.\n    {\n    \t//dist += noise1d(slant*4.0+1.333)*0.1 + noise1d(slant*8.0+1.333)*0.05;\n    \tdist += noiseValue(vec3(slant)*4.0)*0.1 + noiseValue(vec3(slant)*8.0)*0.05;\n        float snowNoise=0.0;\n   \t\tsnowNoise = noiseValue(p*4.0)*0.5*0.5;\n   \t\tsnowNoise += noiseValue(p*8.0)*0.125*0.25;\n        // prevent crashing on mac/chrome/nvidia\n#ifdef HQ_NOISE\n   \t\tsnowNoise += noiseValue(p*16.0)*0.125*0.0625;\n   \t\tsnowNoise += noiseValue(p*32.0)*0.0625*0.0625;\n#endif\n        //snowNoise -= abs(fract(p.z*0.5-p.y*0.05)-0.5)*2.0;\n        //snowNoise -= 0.95;\n        dist += snowNoise*0.25;\n    }\n    vec2 iceberg = vec2(dist, 0.0);\n    vec2 distAndMat = matmin(water, iceberg);\n    return vec3(distAndMat, water.x - iceberg.x);\n}\n\nvec3 TraceOneRay(vec3 camPos, vec3 rayVec, out vec3 normal, out vec3 distAndMat, out float t) {\n    normal = vec3(0.0);\n    distAndMat = vec3(0.0, -1.0, 1000.0);  // Distance and material\n    vec3 finalColor = vec3(0.0);\n\t// ----------------------------- Ray march the scene ------------------------------\n\tt = 0.0;\n\tconst float maxDepth = 32.0; // farthest distance rays will travel\n\tvec3 pos = vec3(0.0);\n    const float smallVal = 0.00625;\n\t// ray marching time\n    for (int i = 210; i >= ZERO_TRICK; i--)\t// This is the count of the max times the ray actually marches.\n    {\n        // Step along the ray.\n        pos = (camPos + rayVec * t);\n        // This is _the_ function that defines the \"distance field\".\n        // It's really what makes the scene geometry. The idea is that the\n        // distance field returns the distance to the closest object, and then\n        // we know we are safe to \"march\" along the ray by that much distance\n        // without hitting anything. We repeat this until we get really close\n        // and then break because we have effectively hit the object.\n        distAndMat = DistanceToObject(pos);\n\n        // move down the ray a safe amount\n        t += distAndMat.x;\n        if (i == 0) t = maxDepth+0.01;\n        // If we are very close to the object, let's call it a hit and exit this loop.\n        if ((t > maxDepth) || (abs(distAndMat.x) < smallVal)) break;\n    }\n\n\t// --------------------------------------------------------------------------------\n\t// Now that we have done our ray marching, let's put some color on this geometry.\n\t// If a ray actually hit the object, let's light it.\n    if (t <= maxDepth)\n\t{\n        float dist = distAndMat.x;\n        // calculate the normal from the distance field. The distance field is a volume, so if you\n        // sample the current point and neighboring points, you can use the difference to get\n        // the normal.\n        vec3 smallVec = vec3(smallVal, 0, 0);\n        vec3 normalU = vec3(dist - DistanceToObject(pos - smallVec.xyy).x,\n                           dist - DistanceToObject(pos - smallVec.yxy).x,\n                           dist - DistanceToObject(pos - smallVec.yyx).x);\n        //vec3 normalU = vec3(0.0);\n        //for( int i=ZERO_TRICK; i<4; i++ )\n        //{\n        //    vec3 e = 0.5773*(2.0*vec3((((i+3)>>1)&1),((i>>1)&1),(i&1))-1.0);\n        //    float dto = DistanceToObject(pos+0.0005*e).x;\n        //    normalU += e*dto;\n        //}\n        normal = normalize(normalU);\n\n        // Giant hack for border between water and snow to look nice.\n        if (abs(distAndMat.z) < smallVal*0.25) normal = vec3(0.0, 0.2, 0.0);\n\n        // calculate 2 ambient occlusion values. One for global stuff and one\n        // for local stuff\n        float ambientS = 1.0;\n        vec3 distAndMatA = DistanceToObject(pos + normal * 0.4);\n        ambientS *= saturate(distAndMatA.x*2.5);\n        distAndMatA = DistanceToObject(pos + normal * 0.8);\n        ambientS *= saturate(distAndMatA.x*1.25);\n        float ambient = ambientS;\n        ambient *= saturate(DistanceToObject(pos + normal * 1.6).x*1.25*0.5);\n        ambient *= saturate(DistanceToObject(pos + normal * 3.2).x*1.25*0.25);\n        //ambient = max(0.05, pow(ambient, 0.5));\t// tone down ambient with a pow and min clamp it.\n        ambient = max(0.15, ambient);\n        ambient = saturate(ambient);\n\n        // Trace a ray toward the sun for sun shadows\n        float sunShadow = 1.0;\n        float iter = 0.2;\n\t\tfor (int i = ZERO_TRICK; i < 10; i++)\n        {\n            float tempDist = DistanceToObject(pos + sunDir * iter).x;\n\t        sunShadow *= saturate(tempDist*10.0);\n            if (tempDist <= 0.0) break;\n            //iter *= 1.5;\t// constant is more reliable than distance-based\n            iter += max(0.1, tempDist)*1.2;\n        }\n        sunShadow = saturate(sunShadow);\n\n        // Trace a ray through the solid for sub-surface scattering\n        float scattering = 1.0;\n        iter = 0.05;\n\t\tfor (int i = ZERO_TRICK; i < 8; i++)\n        {\n            float tempDist = -DistanceToObject(pos - normal * iter).x;\n\t        scattering *= saturate(tempDist*10.0);\n            if (tempDist <= 0.0) break;\n            //iter *= 1.5;\t// constant is more reliable than distance-based\n            iter += max(0.001, tempDist);//*0.6;\n        }\n        scattering = saturate(scattering);\n        //scattering = (1.0-sunShadow)*(1.0-length(sunDir * iter));\n        scattering = saturate(1.0-iter);\n\n        // calculate the reflection vector for highlights\n        vec3 ref = reflect(rayVec, normal);\n\n        // ------ Calculate texture color  ------\n        vec3 texColor = vec3(0.0);\n        // Underwater green glow\n        float fade = 1.0-saturate(abs(distAndMat.z)*3.5);\n        vec3 greenFade = mix(vec3(0.1, 0.995, 0.65)*0.95, vec3(0.75, 1.0, 1.0), fade*fade);\n        texColor += greenFade * fade;\n        texColor *= 0.75;\n        // iceberg\n        if (distAndMat.y == 0.0) {\n            texColor = vec3(1.0);\n        }\n        texColor = saturate(texColor);\n\n        // ------ Calculate lighting color ------\n        // Start with sun color, standard lighting equation, and shadow\n        vec3 lightColor = vec3(14.0)*sunCol * saturate(dot(sunDir, normal)) * (sunShadow*0.7+0.3);\n        // weighted average the near ambient occlusion with the far for just the right look\n\n        // apply the light to the texture.\n        finalColor = texColor * lightColor;\n        vec3 underwaterGlow = vec3(0.002, 0.6, 0.51);\n        // water\n        if (distAndMat.y == 1.0) {\n\t        finalColor += underwaterGlow*0.25 * length(normalU)*80.0;\n\t        finalColor += vec3(0.02, 0.5, 0.71)*0.35 * saturate(1.0-ambient);\n            finalColor += skyCol*0.02 * ambientS;\n        }\n        // iceberg\n        if (distAndMat.y == 0.0) {\n            float fade = saturate(1.0-pos.y);\n\t        // Add sky color\n            finalColor += (skyCol*0.6 + horizonCol*0.4)*1.5 * saturate(normal.y *0.5+0.5);\n\t\t\tvec3 rotPos = RotateZ(pos, sin(localTime)*sway);\n            float noiseScatter = 0.0;\n            float tempScale = 1.0;\n            for (int i = 0; i < 3; i++) {\n                noiseScatter += noiseValue(rotPos*32.0/tempScale)*0.25*tempScale;\n                tempScale *= 2.0;\n            }\n//            noiseValue(rotPos*32.0)*0.25 +\n  //              noiseValue(rotPos*16.0)*0.5 +\n    //            noiseValue(rotPos*8.0)*1.0;\n\t        finalColor += groundCol * 0.5 * max(-normal.y*0.5+0.5, 0.0) * (noiseScatter*0.3+0.6);\n            finalColor += underwaterGlow * 0.35 * saturate(0.5-saturate(abs(pos.y*3.0)));\n            finalColor += vec3(0.01, 0.55, 0.7) * saturate(scattering-sunShadow*0.3)*0.25;\n            finalColor = mix((underwaterGlow + vec3(0.5, 0.9, 0.8))*0.5, finalColor, saturate(distAndMat.z*64.0)*0.75+0.25);\n            finalColor *= 0.7;\n        }\n\n        // visualize length of gradient of distance field to check distance field correctness\n        //finalColor = vec3(0.5) * (length(normalU) / smallVec.x);\n\t}\n    else\n    {\n        // Our ray trace hit nothing, so draw background.\n        finalColor = GetEnvMap(rayVec, sunDir);\n        distAndMat.y = -1.0;\n    }\n    return finalColor;\n}\n\n// Input is UV coordinate of pixel to render.\n// Output is RGB color.\nvec3 RayTrace(in vec2 fragCoord )\n{\n    fade = 1.0;\n\n\tvec3 camPos, camUp, camLookat;\n\t// ------------------- Set up the camera rays for ray marching --------------------\n    // Map uv to [-1.0..1.0]\n\tvec2 uv = fragCoord.xy/iResolution.xy * 2.0 - 1.0;\n    uv /= 2.0;  // zoom in\n\n#ifdef MANUAL_CAMERA\n    // Camera up vector.\n\tcamUp=vec3(0,1,0);\n\n\t// Camera lookat.\n\tcamLookat=vec3(0,0,0);\n\n    // debugging camera\n    float mx=-iMouse.x/iResolution.x*PI*2.0;\n\tfloat my=iMouse.y/iResolution.y*3.14*0.95 + PI/2.0;\n\tcamPos = vec3(cos(my)*cos(mx),sin(my),cos(my)*sin(mx))*5.0;\n#else\n    // Do the camera fly-by animation and different scenes.\n    // Time variables for start and end of each scene\n    const float t0 = 0.0;\n    const float t1 = 12.0;\n    const float t2 = 20.0;\n    const float t3 = 38.0;\n    // Repeat the animation after time t3\n    localTime = fract(localTime / t3) * t3;\n    if (localTime < t1)\n    {\n        float time = localTime - t0;\n        float alpha = time / (t1 - t0);\n        fade = saturate(time);\n        fade *= saturate(t1 - localTime);\n        camPos = vec3(0.0, 0.4, -8.0);\n        camPos.x -= smoothstep(0.0, 1.0, alpha) * 2.0;\n        camPos.y += smoothstep(0.0, 1.0, alpha) * 2.0;\n        camPos.z += smoothstep(0.0, 1.0, alpha) * 4.0;\n        camUp=vec3(0,1,0);\n        camLookat=vec3(0,-0.5,0.5);\n        camLookat.y -= smoothstep(0.0, 1.0, alpha) * 0.5;\n    } else if (localTime < t2)\n    {\n        float time = localTime - t1;\n        float alpha = time / (t2 - t1);\n        fade = saturate(time);\n        fade *= saturate(t2 - localTime);\n        camPos = vec3(2.0, 4.3, -0.5);\n        camPos.y -= alpha * 3.5;\n        camPos.x = sin(alpha*1.0) * 9.2;\n        camPos.z = cos(alpha*1.0) * 6.2;\n        camUp=normalize(vec3(0,1,-0.005 + alpha * 0.005));\n        camLookat=vec3(0,-1.5,0.0);\n        camLookat.y += smoothstep(0.0, 1.0, alpha) * 1.5;\n    } else if (localTime < t3)\n    {\n        float time = localTime - t2;\n        float alpha = time / (t3 - t2);\n        fade = saturate(time);\n        fade *= saturate(t3 - localTime);\n        camPos = vec3(-9.0, 1.3, -10.0);\n        //camPos.y -= alpha * 8.0;\n        camPos.x += alpha * 14.0;\n        camPos.z += alpha * 7.0;\n        camUp=normalize(vec3(0,1,0.0));\n        camLookat=vec3(0.0,0.0,0.0);\n    }\n#endif\n\n\t// Camera setup for ray tracing / marching\n\tvec3 camVec=normalize(camLookat - camPos);\n\tvec3 sideNorm=normalize(cross(camUp, camVec));\n\tvec3 upNorm=cross(camVec, sideNorm);\n\tvec3 worldFacing=(camPos + camVec);\n\tvec3 worldPix = worldFacing + uv.x * sideNorm * (iResolution.x/iResolution.y) + uv.y * upNorm;\n\tvec3 rayVec = normalize(worldPix - camPos);\n\n\tvec3 finalColor = vec3(0.0);\n\n    vec3 normal;\n    vec3 distAndMat;\n    float t;\n    finalColor = TraceOneRay(camPos, rayVec, normal, distAndMat, t);\n    float origDelta = distAndMat.z;\n    if (distAndMat.y == 1.0) {\n        vec3 ref = normalize(reflect(rayVec, normal));\n        ref.y = abs(ref.y);\n        vec3 newStartPos = (camPos + rayVec * t) + normal * 0.02; // nudge away.\n        float fresnel = saturate(1.0 - dot(-rayVec, normal));\n        fresnel = fresnel * fresnel * fresnel * fresnel * fresnel * fresnel;\n        fresnel = mix(0.05, 0.9, fresnel);\n        vec3 refColor = TraceOneRay(newStartPos, ref, normal, distAndMat, t);\n\t    finalColor += refColor * fresnel;\n    }\n\n    // vignette?\n    finalColor *= vec3(1.0) * saturate(1.0 - length(uv/2.5));\n    finalColor *= exposure;\n\n\t// output the final color without gamma correction - will do gamma later.\n\treturn vec3(clamp(finalColor, 0.0, 1.0));//*saturate(fade));\n}\n\n#ifdef NON_REALTIME_HQ_RENDER\n// This function breaks the image down into blocks and scans\n// through them, rendering 1 block at a time. It's for non-\n// realtime things that take a long time to render.\n\n// This is the frame rate to render at. Too fast and you will\n// miss some blocks.\nconst float blockRate = 20.0;\nvoid BlockRender(in vec2 fragCoord)\n{\n    // blockSize is how much it will try to render in 1 frame.\n    // adjust this smaller for more complex scenes, bigger for\n    // faster render times.\n    const float blockSize = 64.0;\n    // Make the block repeatedly scan across the image based on time.\n    float frame = floor(iTime * blockRate);\n    vec2 blockRes = floor(iResolution.xy / blockSize) + vec2(1.0);\n    // ugly bug with mod.\n    //float blockX = mod(frame, blockRes.x);\n    float blockX = fract(frame / blockRes.x) * blockRes.x;\n    //float blockY = mod(floor(frame / blockRes.x), blockRes.y);\n    float blockY = fract(floor(frame / blockRes.x) / blockRes.y) * blockRes.y;\n    // Don't draw anything outside the current block.\n    if ((fragCoord.x - blockX * blockSize >= blockSize) ||\n    \t(fragCoord.x - (blockX - 1.0) * blockSize < blockSize) ||\n    \t(fragCoord.y - blockY * blockSize >= blockSize) ||\n    \t(fragCoord.y - (blockY - 1.0) * blockSize < blockSize))\n    {\n        discard;\n    }\n}\n#endif\n\nvoid mainImage( out vec4 fragColor, in vec2 fragCoord )\n{\n#ifdef NON_REALTIME_HQ_RENDER\n    // Optionally render a non-realtime scene with high quality\n    BlockRender(fragCoord);\n#endif\n\n    // Do a multi-pass render\n    vec3 finalColor = vec3(0.0);\n#ifdef NON_REALTIME_HQ_RENDER\n    for (float i = 0.0; i < antialiasingSamples; i++)\n    {\n        const float motionBlurLengthInSeconds = 1.0 / 60.0;\n        // Set this to the time in seconds of the frame to render.\n\t    localTime = frameToRenderHQ;\n        // This line will motion-blur the renders\n        localTime += Hash11(v21(fragCoord + seed)) * motionBlurLengthInSeconds;\n        // Jitter the pixel position so we get antialiasing when we do multiple passes.\n        vec2 jittered = fragCoord.xy + vec2(\n            Hash21(fragCoord + seed),\n            Hash21(fragCoord*7.234567 + seed)\n            );\n        // don't antialias if only 1 sample.\n        if (antialiasingSamples == 1.0) jittered = fragCoord;\n        // Accumulate one pass of raytracing into our pixel value\n\t    finalColor += RayTrace(jittered);\n        // Change the random seed for each pass.\n\t    seed *= 1.01234567;\n    }\n    // Average all accumulated pixel intensities\n    finalColor /= antialiasingSamples;\n#else\n    // Regular real-time rendering\n    localTime = iTime;\n    finalColor = RayTrace(fragCoord);\n#endif\n\n    fragColor = vec4(sqrt(clamp(finalColor, 0.0, 1.0)),1.0);\n}\n\n\n",
                "description": "",
                "inputs": [],
                "name": "Image",
                "outputs": [
                    {
                        "channel": 0,
                        "id": 37
                    }
                ],
                "type": "image"
            }
        ],
        "ver": "0.1"
    }
}