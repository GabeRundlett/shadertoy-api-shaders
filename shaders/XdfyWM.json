{
    "Shader": {
        "info": {
            "date": "1489665830",
            "description": "Bump mapped, smooth Voronoi layers, sectioned off into fractional contours. Basically, just an excuse to make something shiny. :)",
            "flags": 0,
            "hasliked": 0,
            "id": "XdfyWM",
            "likes": 67,
            "name": "Alloy Plated Voronoi",
            "published": 3,
            "tags": [
                "voronoi",
                "bump",
                "metal",
                "alloy"
            ],
            "usePreview": 0,
            "username": "Shane",
            "viewed": 4379
        },
        "renderpass": [
            {
                "code": "/*\n\n\tAlloy Plated Voronoi\n\t--------------------\n\n    This was originally a rigid scrolling example, but I thought the movement looked more \n\tinteresting. Obviously, hard, fluid alloy doesn't make a lot of physical sense, so if the \n\tsituation really messes with your sense of correctness, uncomment the \"RIGID_SCOLL\" \n\tdefine... or just pretend it's futuristic alien alloy from another dimension. :D\n\n    From a technical perspective, it's a couple of smooth Voronoi layers (based on IQ's\n\tarticle below), split into fractional contours then bump mapped. Hardly cutting edge,\n    and\tnot all that exciting. One of the things that did require some effort was producing \n\tthe succinct smooth borders with enough quality to allow for pronounced bump mapping\n\twith minimal artifacts. The results are sufficient, but there are probably more efficent \n\tways to achieve the same.\n\n\tI was going for a tarnished silver kind of look. Not sure whether it worked, but it looks\n\tshiny, so that's good enough for me. :) When I first started taking an interest in graphics, \n\tmy idea of producing a metallic looking object was to render it flat grey, then add some \n\tambient lighting... Yes, I was \"that\" naive. :D\n\n\tThese days, I apply a little more science, but a lot of it is still made up. Specular \n\tlighting and fake reflections help add to the illusion, but I find that\tramping the diffuse \n\tlight to a few powers tends to work best. Also, to give the light more angularity, I \n\taccentuated the bump mapping more than I usually would.\n\n\t\n\t\n\n\tSimilar Examples:\n\n\tVoronoi - smooth - iq\n\thttps://www.shadertoy.com/view/ldB3zc\n\n    // Great accompanying article.\n    https://iquilezles.org/articles/smoothvoronoi\n\n\n    // A cleaner, more simplistic rendering.\n\tSmooth Voronoi Contours - Shane\n    https://www.shadertoy.com/view/4sdXDX\n\n\n*/\n\n//#define RIGID_SCROLL\n\n// Gradient factor. See the \"func\" function. It's a fudge factor used to make the contour lines appear \n// unison in width. It's a mildly expensive calulation, and I'm reusing it, so it's global. I'll try to \n// localize it later.\nfloat gF; \n\n//float sFract(float x, float sm){ float fx = fract(x); return fx - smoothstep(fwidth(x)*sm, 0., 1. - fx); }\n//float sFract(float x, float sm){ float fx = fract(x); return min(fx, fx*(1. - fx)/gF/sm); }//fwidth(x)\n\n// Based on Ollj's smooth \"fract\" formula.\nfloat sFract(float x, float sm){\n    \n    // Extra smoothing factor. \"1\" is the norm.\n    const float sf = .5; \n    \n    // The hardware \"fwidth\" is cheap, but you could take the expensive route and\n    // calculate it by hand if more quality was required.\n    vec2 u = vec2(x, fwidth(x)*sf*sm);\n    \n    // Ollj's original formula with a transcendental term omitted.\n    u.x = fract(u.x);\n    u += (1. - 2.*u)*step(u.y, u.x);\n    return clamp(1. - u.x/u.y, 0., 1.); // Cos term ommitted.\n}\n\n\n\nfloat sFloor(float x){ return x - sFract(x, 1.); } // Only works for nonnegative \"x,\" which is fine here.\n\n\n// Standard hue rotation formula with a bit of streamlining. \nvec3 rotHue(vec3 p, float a){\n\n    vec2 cs = sin(vec2(1.570796, 0) + a);\n\n    mat3 hr = mat3(0.299,  0.587,  0.114,  0.299,  0.587,  0.114,  0.299,  0.587,  0.114) +\n        \t  mat3(0.701, -0.587, -0.114, -0.299,  0.413, -0.114, -0.300, -0.588,  0.886) * cs.x +\n        \t  mat3(0.168,  0.330, -0.497, -0.328,  0.035,  0.292,  1.250, -1.050, -0.203) * cs.y;\n\t\t\t\t\t\t\t \n    return clamp(p*hr, 0., 1.);\n}\n\n\n// Standard 2x2 hash algorithm.\nvec2 hash22(vec2 p) {\n    \n    // Faster, but probaly doesn't disperse things as nicely as other methods.\n    float n = sin(dot(p, vec2(41, 289)));\n    p = fract(vec2(2097152, 262144)*n);\n    #ifdef RIGID_SCROLL\n    return p - .5;\n    #else\n    return cos(p*6.283 + iTime)*.5;\n    //return abs(fract(p+ iTime*.25)-.5)*2. - .5; // Snooker.\n    //return abs(cos(p*6.283 + iTime))*.5; // Bounce.\n    #endif\n\n}\n\n\n// vec3 to vec3 hash algorithm.\nvec3 hash33(vec3 p) { \n\n    // Faster, but doesn't disperse things quite as nicely as the block below it. However, when framerate\n    // is an issue, and it often is, this is the one to use. Basically, it's a tweaked amalgamation I put\n    // together, based on a couple of other random algorithms I've seen around... so use it with caution,\n    // because I make a tonne of mistakes. :)\n    float n = sin(dot(p, vec3(7, 157, 113)));    \n    return fract(vec3(2097152, 262144, 32768)*n)*2. - 1.; // return fract(vec3(64, 8, 1)*32768.0*n)*2.-1.; \n\n    // I'll assume the following came from IQ.\n    //p = vec3( dot(p, vec3(127.1, 311.7, 74.7)), dot(p, vec3(269.5, 183.3, 246.1)), dot(p, vec3(113.5, 271.9, 124.6)));\n    //return (fract(sin(p)*43758.5453)*2. - 1.);\n\n}\n\n\n\n// Cheap, streamlined 3D Simplex noise... of sorts. I cut a few corners, so it's not perfect, but it's\n// artifact free and does the job. I gave it a different name, so that it wouldn't be mistaken for\n// the real thing.\n// \n// Credits: Ken Perlin, the inventor of Simplex noise, of course. Stefan Gustavson's paper - \n// \"Simplex Noise Demystified,\" IQ, other \"ShaderToy.com\" people, etc.\nfloat tetraNoise(in vec3 p){\n    \n    // Skewing the cubic grid, then determining the first vertice and fractional position.\n    vec3 i = floor(p + dot(p, vec3(0.333333)) );  p -= i - dot(i, vec3(0.166666)) ;\n    \n    // Breaking the skewed cube into tetrahedra with partitioning planes, then determining which side of the \n    // intersecting planes the skewed point is on. Ie: Determining which tetrahedron the point is in.\n    vec3 i1 = step(p.yzx, p), i2 = max(i1, 1.0-i1.zxy); i1 = min(i1, 1.0-i1.zxy);    \n    \n    // Using the above to calculate the other three vertices. Now we have all four tetrahedral vertices.\n    vec3 p1 = p - i1 + 0.166666, p2 = p - i2 + 0.333333, p3 = p - 0.5;\n  \n\n    // 3D simplex falloff.\n    vec4 v = max(0.5 - vec4(dot(p,p), dot(p1,p1), dot(p2,p2), dot(p3,p3)), 0.0);\n    \n    // Dotting the fractional position with a random vector generated for each corner -in order to determine \n    // the weighted contribution distribution... Kind of. Just for the record, you can do a non-gradient, value \n    // version that works almost as well.\n    vec4 d = vec4(dot(p, hash33(i)), dot(p1, hash33(i + i1)), dot(p2, hash33(i + i2)), dot(p3, hash33(i + 1.)));\n    \n     \n    // Simplex noise... Not really, but close enough. :)\n    return clamp(dot(d, v*v*v*8.)*1.732 + .5, 0., 1.); // Not sure if clamping is necessary. Might be overkill.\n\n}\n\n\n\n// Smooth Voronoi. I'm not sure who came up with the original, but I think IQ\n// was behind this particular algorithm. It's just like the regular Voronoi\n// algorithm, but instead of determining the minimum distance, you accumulate\n// values - analogous to adding metaball field values. The result is a nice\n// smooth pattern. The \"falloff\" variable is a smoothing factor of sorts.\n//\nfloat smoothVoronoi(vec2 p, float falloff) {\n\n    vec2 ip = floor(p); p -= ip;\n\t\n\tfloat d = 1., res = 0.;\n\t\n\tfor(int i=-1; i<=2; i++) {\n\t\tfor(int j=-1; j<=2; j++) {\n            \n\t\t\tvec2 b = vec2(i, j);\n            \n\t\t\tvec2 v = b - p + hash22(ip + b);\n            \n\t\t\td = max(dot(v,v), 1e-8);\n\t\t\t\n\t\t\tres += 1./pow(d, falloff );\n            //res += exp( -16.*d ); // Alternate version.\n\t\t}\n\t}\n\n\treturn pow(1./res, .5/falloff);\n    //return clamp((-(1./16.)*log(res) + .1)/1.1, 0., 1.); // Alternate version.\n}\n\n\n\n// 2D function we'll be creating the fractional contours for. \nfloat func2D(vec2 p){\n    \n    #ifdef RIGID_SCROLL\n    p += vec2(-.2, 0)*iTime; // Scrolling.\n    #endif\n    \n    return smoothVoronoi(p*2., 4.)*.66 + smoothVoronoi(p*6., 4.)*.34;\n    \n}\n\n// For fractional countours, something like \"fract(func(p)*layers\" will work, but the results \n// are aliased, so a smooth \"fract\" function needs to be applied. For nice, even contour lines,\n// the functional curvature needs to be taken into account. Hence the \"fxr\" and \"fyb\" samples.\nfloat func(vec2 p){\n    \n    float f = func2D(p); // Function value.\n   \n    // Samples in the X and Y directions to even up the contour lines according to curvature.\n    vec2 e = vec2(8./iResolution.y, 0);\n    float fxr = func2D(p - e.xy);\n    float fyb = func2D(p - e.yx);\n \n    // Gradient factor. Four samples would be better, but I'm trying to save some calculations.\n    // I made \"gF\" global, for reuse purposes, but I'll try to rectify that later. See the \"sFract\"\n    // function. Press pause, then set \"gF\" to a constant, like \".04,\" to see what it does.\n    // This example would be a lot faster if \"gF\" was set to a constant (since the two samples \n    // above wouldn't be necessary), so if you like that aesthetic more, then that's the way to go.\n    gF = length(f - vec2(fxr, fyb)); \n    \n    const float palNum = 12.; // 12 contour lines.\n    return sFract(f*palNum, 4.); // 4 is a smoothing factor. Getting the balance right can be frustrating.   \n    \n}\n\n// Simple environment mapping. Pass the reflected vector in and create some\n// colored noise with it.\nvec3 envMap(vec3 rd){\n    \n   \n    float c = tetraNoise(rd*3.)*.57 + tetraNoise(rd*6.)*.28 + tetraNoise(rd*12.)*.15; // Noise value.\n    c = smoothstep(.4, 1., c); // Darken and add contast for more of a spotlight look.\n    \n    vec3 col = vec3(c*c*c, c*c, c); // Simple, cool coloring.\n    //vec3 col = vec3(min(c*1.5, 1.), pow(c, 2.5), pow(c, 12.)); // Warm color.\n    \n    // Mix in the reverse color to tone it down and return.\n    return mix(col, col.zxy, rd*.25 + .25); \n    \n}\n\n\n\nvoid mainImage(out vec4 fragColor, in vec2 fragCoord){\n\n    // Screen coordinates.\n\tvec2 u = (fragCoord.xy - iResolution.xy*.5)/iResolution.y;\n    \n    // Function value.\n    float f = func(u);\n    float ssd = func2D(u); // Saving the unpalettized noise value to add a little gradient to the color, etc.\n    \n    // Four sample values around the original. Used for edging and highlighting. Note the \"1.5\" factor in a \n    // couple of samples. I was playing around and liked it more that way, but you can take it out if you want.\n    vec2 e = vec2(2./450., 0);\n    float fxl = func(u + e.xy*1.5);\n    float fxr = func(u - e.xy);\n    float fyt = func(u + e.yx*1.5);\n    float fyb = func(u - e.yx);\n  \n    // Colorizing the the function value.\n    vec3 col = vec3(.5);\n    \n    // Applying some color and hue rotation based on fractional layer and position. Designed to coincide\n    // with the \"func\" function.\n    const float palNum2 = 12.;\n    float fi = (1. - clamp(sFloor(ssd*(palNum2))/(palNum2 - 1.), 0., 1.));\n    fi = mod(fi, 4./12.);\n    \n    if(fi>3./12.) {\n        // Extra color layers. I found it a bit much.\n        //col *= vec3(1, .18, .28);\n        //col = rotHue(col, mod(iTime/3. + 3.14159, 6.2831853) + (length(u*vec2(3., 5.))));        \n        col *= .25;                   \n    }\n    else if(fi>2./12.) {\n        \n        col *= vec3(1, .18, .28);\n        col = rotHue(col, mod(iTime/4., 6.2831853) + (length(u*vec2(2.5, 4.5))));// + iTime*.5\n    }\n    \n    // Adding a bit of noise for a bit more authenticity.\n    vec3 u3 = vec3(u, f); // Fake height, \"ssd,\" according to function value.\n    #ifdef RIGID_SCROLL\n    u3.xy += vec2(-.2, 0)*iTime; // Scrolling.\n    #endif\n    col = min(col*.8 + tetraNoise(u3*128.)*.4, 1.);\n    \n\n   \n    // Extra highlighting to shine up the edges. Purely for aesthetics. Not based on science. :)\n    col += vec3(.5, .7, 1)*(max(f - fyt, 0.) + max(f - fxl, 0.))*1.*ssd*2.;\n\n    \n    vec3 rd = normalize(vec3(u, 1)); // Unit direction vector.\n    vec3 n = normalize(vec3(0, 0, -1) + vec3(fxr - fxl, fyb - fyt, 0)/e.x/1.4*.01); // Bumped normal.\n    vec3 ld = (vec3(.25, .25, -1.) - vec3(u, 0)); // Light direction - Position minus surface point.\n    \n    float dist = length(ld); // Light distance.\n    float atten = 1./(1. + dist*dist*.25); // Light attenuation.\n    \n    ld /= dist; // Normalizing the light dirction vector.\n    \n    \n    float diff = max(dot(ld, n), 0.); // Diffuse.\n    diff = pow(diff, 4.)*.66 + pow(diff, 8.)*.34; // Ramped diffuse - for shininess.\n    float spec = pow(max(dot(reflect(ld, n), rd), 0.), 8.); // Specular.\n    float fres = pow(clamp(dot(rd, n) + 1., 0., 1.), 3.); // Fresnel.\n    \n    // Combining the above terms to produce the lit color.\n    col = col*(diff*2. + .125) + vec3(1, .7, .3)*spec*2. + vec3(.25, .5, 1)*fres*2.;\n    //col += vec3(.5, .7, 1)*diff*diff*.5; // Too much shine. :)\n\n\n    // Fake environment mapping for that reflective look.\n    col += (col*.65 + .35)*envMap(reflect(rd, vec3(0, 0, -1)*.25 + n*.75))*2.; \n\n    // Attenuating, then adding some brown shadowing for a subtle tarnished look.\n    col *= atten*(vec3(f, pow(f, 1.1), pow(f, 1.2))*.85 + .15); \n    \n    \n    //col *= vec3(1.35, .8, .5); // Bronze, copper... kind of.\n  \n    \n    // Subtle, bluish vignette.\n    //u = fragCoord/iResolution.xy;\n    //col = mix(vec3(0, .1, 1), col, pow( 16.0*u.x*u.y*(1.0-u.x)*(1.0-u.y) , .125)*.15 + .85);\n\n \t\n    // Rough gamma correction.\n    fragColor = vec4(sqrt(clamp(col, 0., 1.)), 1);\n    \n}",
                "description": "",
                "inputs": [],
                "name": "Image",
                "outputs": [
                    {
                        "channel": 0,
                        "id": 37
                    }
                ],
                "type": "image"
            }
        ],
        "ver": "0.1"
    }
}